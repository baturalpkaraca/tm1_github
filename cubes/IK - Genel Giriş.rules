SKIPCHECK;

[] = IF(ELPAR('Version', !Version, 1) @= 'Aktif', CONTINUE, STET);

['Ortalama Ücret'] = C: ['Brüt Ücret'] \ ['Çalışan Sayısı'];

##Ortalama ücret.
['Ortalama Ücret', { 'Müşteri Danışmanı', 'Kasa Ofis Müşteri Danışmanı', 'Kasa Müşteri Danışmanı', 'Hizmetler Müşteri Danışmanı', 'Satış Müşteri Danışmanı', 'Proje Müşteri Danışmanı' }] = N: 
	DB( 'IK_V_P', !Version, !Period, 'Asgari Ücret'  );

#Brüt ücret.
[{ 'Stajyer Merkez', 'Stajyer Mağaza' }, 'Full Time', 'Ortalama Ücret'] = N: 
	DB( 'IK_V_P', !Version, !Period, 'Asgari Ücret'  ) * 
    DB( 'IK_V_P', !Version, !Period, 'Stajyer Ücret Oranı %'  ) * 0.7149;

['Brüt Ücret'] = N: IF(!Çalışan_Alt_Grupları @= 'Part Time', ['Ortalama Ücret'] * ['Çalışan Sayısı'] * 2 / 3, ['Ortalama Ücret'] * ['Çalışan Sayısı']);

['Koç Ailem Brüt'] = N: IF(!Çalışan_Alt_Grupları @= 'Part Time', (2 / 3) * 
    DB('IK - Genel Parametreler', 'Koç Ailem Fiyat', !Version, 'Tutar' ) * 
    DB( 'IK - Genel Giriş', !Masraf_Kar_Merkezleri, !Unvanlar, !Çalışan_Alt_Grupları, !Version, !Period, 'Çalışan Sayısı'  ), 
    DB('IK - Genel Parametreler', 'Koç Ailem Fiyat', !Version, 'Tutar' ) *
    DB( 'IK - Genel Giriş', !Masraf_Kar_Merkezleri, !Unvanlar, !Çalışan_Alt_Grupları, !Version, !Period, 'Çalışan Sayısı'  ));

['Koç Topluluğu Spor Kulübü Brüt'] = N: IF(!Çalışan_Alt_Grupları @= 'Part Time', (2 / 3) * 
    DB('IK - Genel Parametreler', 'Koç Topluluğu Spor Kulübü Fiyat', !Version, 'Tutar' ) * 
    DB( 'IK - Genel Giriş', !Masraf_Kar_Merkezleri, !Unvanlar, !Çalışan_Alt_Grupları, !Version, !Period, 'Çalışan Sayısı'  ),
    DB('IK - Genel Parametreler', 'Koç Topluluğu Spor Kulübü Fiyat', !Version, 'Tutar' ) *
    DB( 'IK - Genel Giriş', !Masraf_Kar_Merkezleri, !Unvanlar, !Çalışan_Alt_Grupları, !Version, !Period, 'Çalışan Sayısı'  ));

#Stajyerlere brüt ücret dışında bir şey gitmeyecek.
[{'Stajyer Merkez', 'Stajyer Mağaza'}, 'Full Time', 'Çalışan Sayısı'] = STET;
[{'Stajyer Merkez', 'Stajyer Mağaza'}, 'Part Time', { 'Ortalama Ücret', 'Çalışan Sayısı' }] = N: STET;
[{'Stajyer Merkez', 'Stajyer Mağaza'}] = N: IF( !İK_Genel_Giriş_Meas @<> 'EFT Sayısı', 0, CONTINUE );

#İkramiye.
['İkramiye'] = N: ['Brüt Ücret'] / 3;

#Yakacak.
['Yakacak Birim Fiyat'] = N: IF(
	DB( 'IK - Genel Parametreler', 'Yakacak Birim Fiyat', !Version, 'Part Time İçin Birim Fiyat Hesaplansın mı?'  ) @= 'Evet'
    & !Çalışan_Alt_Grupları @= 'Part Time', DB('IK - Genel Parametreler', 'Yakacak Birim Fiyat', !Version, 'Tutar') * 2 / 3, IF(!Çalışan_Alt_Grupları @= 'Full Time'
	% !Çalışan_Alt_Grupları @= 'Sezonluk', DB('IK - Genel Parametreler', 'Yakacak Birim Fiyat', !Version, 'Tutar'), 0));

['Yakacak'] = N: ['Yakacak Birim Fiyat'] * ['Çalışan Sayısı'];

#İzin Harçlığı.
[{ 'Müşteri Danışmanı', 'Kasa Ofis Müşteri Danışmanı', 'Kasa Müşteri Danışmanı', 'Hizmetler Müşteri Danışmanı', 'Satış Müşteri Danışmanı', 'Proje Müşteri Danışmanı', 'Merkez Uzman' }, 'İzin Harçlığı Birim Fiyat'] = N: 
	IF(DB('IK - Genel Parametreler', 'İzin Harçlığı Birim Fiyat', !Version, 'Part Time İçin Birim Fiyat Hesaplansın mı?') @= 'Evet'
	& !Çalışan_Alt_Grupları @= 'Part Time', DB('IK - Genel Parametreler', 'İzin Harçlığı Birim Fiyat', !Version, 'Tutar') * 2 / 3, 
    IF(!Çalışan_Alt_Grupları @= 'Full Time', DB('IK - Genel Parametreler', 'İzin Harçlığı Birim Fiyat', !Version, 'Tutar'), 0));

['İzin Harçlığı Birim Fiyat'] = N: 0;

['İzin Harçlığı Alan Personel Sayısı'] = N: IF((ELPAR('Unvanlar', !Unvanlar, 1) @<> 'Toplam Müşteri Danışmanı') & !Unvanlar @<> 'Merkez Uzman', 0, CONTINUE);

[{ 'Müşteri Danışmanı', 'Kasa Ofis Müşteri Danışmanı', 'Kasa Müşteri Danışmanı', 'Hizmetler Müşteri Danışmanı', 'Satış Müşteri Danışmanı', 'Proje Müşteri Danışmanı', 'Merkez Uzman' }, 'İzin Harçlığı'] = N: 
	['İzin Harçlığı Birim Fiyat'] * ['İzin Harçlığı Alan Personel Sayısı'];

['İzin Harçlığı'] = N: 0;

#Bayram.
['Bayram Birim Fiyat'] = N: 
	IF(DB('IK - Genel Parametreler', 'Bayram Birim Fiyat', !Version, 'Part Time İçin Birim Fiyat Hesaplansın mı?') @= 'Evet'
	& !Çalışan_Alt_Grupları @= 'Part Time', DB('IK - Genel Parametreler', 'Bayram Birim Fiyat', !Version, 'Tutar') * 2 / 3, IF(!Çalışan_Alt_Grupları @= 'Full Time'
	% !Çalışan_Alt_Grupları @= 'Sezonluk', DB('IK - Genel Parametreler', 'Bayram Birim Fiyat', !Version, 'Tutar'), 0));

['Bayram'] = N: IF(DB( 'IK_V_P', !Version, !Period, 'Bayram Ay Seçimi'  ) @= 'Evet', ['Bayram Birim Fiyat'] * ['Çalışan Sayısı'], 0);

[{ 'Müşteri Danışmanı', 'Kasa Ofis Müşteri Danışmanı', 'Kasa Müşteri Danışmanı', 'Hizmetler Müşteri Danışmanı', 'Satış Müşteri Danışmanı', 'Proje Müşteri Danışmanı', 'Merkez Uzman' }, 'Tahsil Yardımı İlköğretim Birim Fiyat'] = N: 
	IF(DB('IK - Genel Parametreler', 'Tahsil Yardımı İlköğretim Birim Fiyat', !Version, 'Part Time İçin Birim Fiyat Hesaplansın mı?') @= 'Evet'
	& !Çalışan_Alt_Grupları @= 'Part Time', DB('IK - Genel Parametreler', 'Tahsil Yardımı İlköğretim Birim Fiyat', !Version, 'Tutar') * 2 / 3, 
    IF(!Çalışan_Alt_Grupları @= 'Full Time', DB('IK - Genel Parametreler', 'Tahsil Yardımı İlköğretim Birim Fiyat', !Version, 'Tutar'), 0));

['Tahsil Yardımı İlköğretim Birim Fiyat'] = N: 0;

[{ 'Müşteri Danışmanı', 'Kasa Ofis Müşteri Danışmanı', 'Kasa Müşteri Danışmanı', 'Hizmetler Müşteri Danışmanı', 'Satış Müşteri Danışmanı', 'Proje Müşteri Danışmanı', 'Merkez Uzman' }, 'Tahsil Yardımı Lise Birim Fiyat'] = N: 
	IF(DB('IK - Genel Parametreler', 'Tahsil Yardımı Lise Birim Fiyat', !Version, 'Part Time İçin Birim Fiyat Hesaplansın mı?') @= 'Evet'
	& !Çalışan_Alt_Grupları @= 'Part Time', DB('IK - Genel Parametreler', 'Tahsil Yardımı Lise Birim Fiyat', !Version, 'Tutar') * 2 / 3, 
    IF(!Çalışan_Alt_Grupları @= 'Full Time', DB('IK - Genel Parametreler', 'Tahsil Yardımı Lise Birim Fiyat', !Version, 'Tutar'), 0));

['Tahsil Yardımı Lise Birim Fiyat'] = N: 0;

[{ 'Müşteri Danışmanı', 'Kasa Ofis Müşteri Danışmanı', 'Kasa Müşteri Danışmanı', 'Hizmetler Müşteri Danışmanı', 'Satış Müşteri Danışmanı', 'Proje Müşteri Danışmanı', 'Merkez Uzman' }, 'Tahsil Yardımı Yüksekokul Birim Fiyat'] = N: 
	IF(DB('IK - Genel Parametreler', 'Tahsil Yardımı Yüksekokul Birim Fiyat', !Version, 'Part Time İçin Birim Fiyat Hesaplansın mı?') @= 'Evet'
	& !Çalışan_Alt_Grupları @= 'Part Time', DB('IK - Genel Parametreler', 'Tahsil Yardımı Yüksekokul Birim Fiyat', !Version, 'Tutar') * 2 / 3, 
    IF(!Çalışan_Alt_Grupları @= 'Full Time', DB('IK - Genel Parametreler', 'Tahsil Yardımı Yüksekokul Birim Fiyat', !Version, 'Tutar'), 0));

['Tahsil Yardımı Yüksekokul Birim Fiyat'] = N: 0;

['Tahsil Yardımı İlköğretim Alacak Personel Sayısı'] = N: IF((ELPAR('Unvanlar', !Unvanlar, 1) @<> 'Toplam Müşteri Danışmanı') & !Unvanlar @<> 'Merkez Uzman', 0, CONTINUE);

['Tahsil Yardımı Lise Alacak Personel Sayısı'] = N: IF((ELPAR('Unvanlar', !Unvanlar, 1) @<> 'Toplam Müşteri Danışmanı') & !Unvanlar @<> 'Merkez Uzman', 0, CONTINUE);

['Tahsil Yardımı Yüksekokul Alacak Personel Sayısı'] = N: IF((ELPAR('Unvanlar', !Unvanlar, 1) @<> 'Toplam Müşteri Danışmanı') & !Unvanlar @<> 'Merkez Uzman', 0, CONTINUE);

[{ 'Müşteri Danışmanı', 'Kasa Ofis Müşteri Danışmanı', 'Kasa Müşteri Danışmanı', 'Hizmetler Müşteri Danışmanı', 'Satış Müşteri Danışmanı', 'Proje Müşteri Danışmanı', 'Merkez Uzman' }, 'Tahsil Yardımı'] = N: 
	IF(DB( 'IK_V_P', !Version, !Period, 'Tahsil Yardımı Ay Seçimi'  ) @= 'Evet', 
    (['Tahsil Yardımı İlköğretim Birim Fiyat'] * ['Tahsil Yardımı İlköğretim Alacak Personel Sayısı']) + (['Tahsil Yardımı Lise Birim Fiyat'] * 
    ['Tahsil Yardımı Lise Alacak Personel Sayısı']) + (['Tahsil Yardımı Yüksekokul Birim Fiyat'] * ['Tahsil Yardımı Yüksekokul Alacak Personel Sayısı']), 0);

['Tahsil Yardımı'] = N: 0;

#Resmi tatil.
['Günlük Brüt Ücret'] = N: ['Brüt Ücret'] \ 30;

['Resmi Tatilde Çalışacak Personel Oranı'] = C: 0;

['Resmi Tatilde Çalışacak Personel Oranı'] = N: 
	IF(Attrs('Masraf_Kar_Merkezleri', !Masraf_Kar_Merkezleri, 'İK Sınıf') @= 'All Stores' & ATTRS('Unvanlar', !Unvanlar, 'Hesaplama') @= 'Evet',
    DB( 'IK_V_P', !Version, !Period, 'Resmi Tatilde Çalışacak Personel Oranı'  ), 0);

['Resmi Tatillerdeki Çalışma Gün Sayısı'] = C: 0;

['Resmi Tatillerdeki Çalışma Gün Sayısı'] = N: 
	IF(Attrs('Masraf_Kar_Merkezleri', !Masraf_Kar_Merkezleri, 'İK Sınıf') @= 'All Stores'
	& ATTRS('Unvanlar', !Unvanlar, 'Hesaplama') @= 'Evet', DB( 'IK_V_P', !Version, !Period, 'Resmi Tatillerdeki Çalışma Gün Sayısı'), 0);

['Resmi Tatillerdeki Çalışma Ücreti'] = N: IF(!Unvanlar @<> 'Mağaza Müdürü', ['Günlük Brüt Ücret'] * ['Resmi Tatillerdeki Çalışma Gün Sayısı'] * ['Resmi Tatilde Çalışacak Personel Oranı'], 0);

#Çocuk yardımı.
[{ 'Müşteri Danışmanı', 'Kasa Ofis Müşteri Danışmanı', 'Kasa Müşteri Danışmanı', 'Hizmetler Müşteri Danışmanı', 'Satış Müşteri Danışmanı', 'Proje Müşteri Danışmanı', 'Merkez Uzman' }, 'Çocuk Yardımı Birim Fiyat'] = N: 
	IF(DB('IK - Genel Parametreler', 'Çocuk Yardımı Birim Fiyat', !Version, 'Part Time İçin Birim Fiyat Hesaplansın mı?') @= 'Evet'
	& !Çalışan_Alt_Grupları @= 'Part Time', DB('IK - Genel Parametreler', 'Çocuk Yardımı Birim Fiyat', !Version, 'Tutar'), 
    IF(!Çalışan_Alt_Grupları @= 'Full Time', DB('IK - Genel Parametreler', 'Çocuk Yardımı Birim Fiyat', !Version, 'Tutar'), 0));

['Çocuk Yardımı Birim Fiyat'] = N: 0;

['Çocuk Sayısı'] = N: IF((ELPAR('Unvanlar', !Unvanlar, 1) @<> 'Toplam Müşteri Danışmanı') & !Unvanlar @<> 'Merkez Uzman', 0, CONTINUE);

[{ 'Müşteri Danışmanı', 'Kasa Ofis Müşteri Danışmanı', 'Kasa Müşteri Danışmanı', 'Hizmetler Müşteri Danışmanı', 'Satış Müşteri Danışmanı', 'Proje Müşteri Danışmanı', 'Merkez Uzman' }, 'Çocuk Yardımı'] = N: 
	['Çocuk Yardımı Birim Fiyat'] * ['Çocuk Sayısı'];

['Çocuk Yardımı'] = N: 0;

#Kasa tazminatı.
['Kasa Müşteri Danışmanı', 'Kasa Tazminatı Birim Fiyat'] = N: 
	IF(DB('IK - Genel Parametreler', 'Kasa Tazminatı Birim Fiyat', !Version, 'Part Time İçin Birim Fiyat Hesaplansın mı?') @= 'Evet'
	& !Çalışan_Alt_Grupları @= 'Part Time', DB('IK - Genel Parametreler', 'Kasa Tazminatı Birim Fiyat', !Version, 'Tutar') * 2 / 3, 
    IF(!Çalışan_Alt_Grupları @= 'Full Time', DB('IK - Genel Parametreler', 'Kasa Tazminatı Birim Fiyat', !Version, 'Tutar'), 0));

['Kasa Tazminatı Birim Fiyat'] = N: 0;

['Kasa Müşteri Danışmanı', 'Kaza Tazminatı'] = N: ['Kasa Tazminatı Birim Fiyat'] * ['Kasa Tazminatı Alacak Kişi Sayısı'];

['Kaza Tazminatı'] = N: 0;

#Ayni yardım.
['Ayni Yardım Birim Fiyat'] = N: 
	IF(DB( 'IK_V_P', !Version, !Period, 'Ayni Yardım Ay Seçimi') @= 'Evet', IF(DB('IK - Genel Parametreler', 'Ayni Yardım Birim Fiyat', !Version, 'Part Time İçin Birim Fiyat Hesaplansın mı?') @= 'Evet'
	& !Çalışan_Alt_Grupları @= 'Part Time', DB('IK - Genel Parametreler', 'Ayni Yardım Birim Fiyat', !Version, 'Tutar'), IF(!Çalışan_Alt_Grupları @= 'Full Time'
	% !Çalışan_Alt_Grupları @= 'Sezonluk', DB('IK - Genel Parametreler', 'Ayni Yardım Birim Fiyat', !Version, 'Tutar'), 0)), 0);

['Ayni Yardım'] = N: ['Ayni Yardım Birim Fiyat'] * ['Çalışan Sayısı'];

#İşten ayrılanların senelik izin ödemesi.

['Senelik İzin Öd. Gün Sayısı'] = N: 
	IF((ELPAR('Unvanlar', !Unvanlar, 1) @<> 'Toplam Müşteri Danışmanı') & !Unvanlar @<> 'Merkez Uzman', 0, CONTINUE);

['İşten Ayrılan Kişi Sayısı'] = N: 
	IF((ELPAR('Unvanlar', !Unvanlar, 1) @<> 'Toplam Müşteri Danışmanı') & !Unvanlar @<> 'Merkez Uzman', 0, CONTINUE);

[{ 'Müşteri Danışmanı', 'Kasa Ofis Müşteri Danışmanı', 'Kasa Müşteri Danışmanı', 'Hizmetler Müşteri Danışmanı', 'Satış Müşteri Danışmanı', 'Proje Müşteri Danışmanı', 'Merkez Uzman' }, 'Full Time', 'İşten Ayrılanların Senelik İzin Ödemesi'] = N: 
	['İşten Ayrılan Kişi Sayısı'] * ['Senelik İzin Öd. Gün Sayısı'] * ['Ortalama Ücret'] / 30;

['İşten Ayrılanların Senelik İzin Ödemesi'] = N: 0;

#İşten ayrılanların fazla mesai karşılığı.
['Fazla Mesai Saati'] = N: IF((ELPAR('Unvanlar', !Unvanlar, 1) @<> 'Toplam Müşteri Danışmanı') % !Çalışan_Alt_Grupları @<> 'Full Time', 0, CONTINUE);

[{ 'Müşteri Danışmanı', 'Kasa Ofis Müşteri Danışmanı', 'Kasa Müşteri Danışmanı', 'Hizmetler Müşteri Danışmanı', 'Satış Müşteri Danışmanı', 'Proje Müşteri Danışmanı' }, 'Full Time', 'İşten Ayrılanların Fazla Mesai Karş.'] = N: 
	['İşten Ayrılan Kişi Sayısı'] * ['Fazla Mesai Saati'] * ['Ortalama Ücret'] / 225;

['İşten Ayrılanların Fazla Mesai Karş.'] = N: 0;

#Prim.
['Prim'] = N: IF(Attrs('Masraf_Kar_Merkezleri', !Masraf_Kar_Merkezleri, 'İK Sınıf') @<> 'All Stores' & ELPAR('Unvanlar', !Unvanlar, 1) @= 'Toplam Merkez Unvan', STET, CONTINUE);

['Prim'] = N: IF(DB( 'MM_V', !Masraf_Kar_Merkezleri, !Version, 'Prim Hesaplama Seçim'  ) @= 'Hayır', STET, CONTINUE);

['Çalışan Sayısı (Prim İçin)'] = N: IF(DB( 'MM_V', !Masraf_Kar_Merkezleri, !Version, 'Prim Hesaplama Seçim'  ) @= 'Evet'
	& ATTRS('Unvanlar', !Unvanlar, 'Hesaplama') @= 'Evet', DB( 'IK - Genel Giriş', !Masraf_Kar_Merkezleri, !Unvanlar, 'TOPLAM', !Version, !Period, 'Çalışan Sayısı'  ), 0);

['Prim'] = N: IF(DB( 'MM_V', !Masraf_Kar_Merkezleri, !Version, 'Prim Hesaplama Seçim'  ) @= 'Evet' & ATTRS('Unvanlar', !Unvanlar, 'Hesaplama') @= 'Evet', 
    DB( 'IK - Genel Parametreler', 'Yıllık Mağaza Prim Tutarı', !Version, 'Tutar'  ) * 
    DB( 'IK_V_P', !Version, !Period, 'Mağaza Prim Dağıtım Anahtarı') * ['Çalışan Sayısı'] \ (['Koçtaş Total ', 'Toplam Mağaza Unvan', 'TOPLAM ÇALIŞAN ALT GRUP', 'Çalışan Sayısı (Prim İçin)']), 0);

#Flextra.
['Flextra Şirket Katkı Payı Birim Fiyat'] = N: 
	IF(DB('IK - Genel Parametreler', 'Flextra Şirket Katkı Payı Birim Fiyat', !Version, 'Part Time İçin Birim Fiyat Hesaplansın mı?') @= 'Evet'
	& !Çalışan_Alt_Grupları @= 'Part Time', DB('IK - Genel Parametreler', 'Flextra Şirket Katkı Payı Birim Fiyat', !Version, 'Tutar'), IF(!Çalışan_Alt_Grupları @= 'Full Time', 
    DB('IK - Genel Parametreler', 'Flextra Şirket Katkı Payı Birim Fiyat', !Version, 'Tutar'), 0));

['Flextra Şirket Katkı Payı Birim Fiyat'] = N: 0;

[{ 'Müşteri Danışmanı', 'Kasa Ofis Müşteri Danışmanı', 'Kasa Müşteri Danışmanı', 'Hizmetler Müşteri Danışmanı', 'Satış Müşteri Danışmanı', 'Proje Müşteri Danışmanı', 'Merkez Uzman' }, 'Full Time', 'Flextra Şirket Katkı Payı'] = N: 
	['Flextra Şirket Katkı Payı Birim Fiyat'] * DB( 'IK - Genel Giriş', !Masraf_Kar_Merkezleri, !Unvanlar, 'Full Time', !Version, !Period, 'Flextra Kişi Sayısı'  );

['Flextra Şirket Katkı Payı'] = N: 0;

['EFT Sayısı'] = N: 
     DB( 'IK - Genel Giriş', !Masraf_Kar_Merkezleri, !Unvanlar, 'TOPLAM', !Version, !Period, 'Çalışan Sayısı'  ) -
    (DB( 'IK - Genel Giriş', !Masraf_Kar_Merkezleri, !Unvanlar, 'Part Time', !Version, !Period, 'Çalışan Sayısı'  ) * 1 \ 3);

['Tamamlayıcı Sağlık Sigortası Birim Fiyat'] = N: DB('IK - Genel Parametreler', 'Tamamlayıcı Sağlık Sigortası Birim Fiyat', !Version, 'Tutar');

['Tamamlayıcı Sağlık Sigortası'] = N: ['Tamamlayıcı Sağlık Sigortası Birim Fiyat'] * ['Tamamlayıcı Sağlık Sigortasından Yararlanan Kişi Sayısı'];

FEEDERS;

['Aktif', 'Çalışan Sayısı'] => ['Brüt Ücret'], ['Günlük Brüt Ücret'], ['Prim'], ['İkramiye'], ['Yakacak'], ['Yakacak Birim Fiyat'], ['Bayram'], ['Bayram Birim Fiyat'], ['Ayni Yardım'], ['Çalışan Sayısı (Prim İçin)'], ['Kasa Tazminatı Alacak Kişi Sayısı'], ['EFT Sayısı'], ['Koç Ailem Brüt'], ['Koç Topluluğu Spor Kulübü Brüt'];

['Aktif', 'İzin Harçlığı Alan Personel Sayısı'] => ['İzin Harçlığı'], ['İzin Harçlığı Birim Fiyat'];

['Aktif', 'Tahsil Yardımı İlköğretim Alacak Personel Sayısı'] => ['Tahsil Yardımı'], ['Tahsil Yardımı İlköğretim Birim Fiyat'];
['Aktif', 'Tahsil Yardımı Lise Alacak Personel Sayısı'] => ['Tahsil Yardımı'], ['Tahsil Yardımı Lise Birim Fiyat'];
['Aktif', 'Tahsil Yardımı Yüksekokul Alacak Personel Sayısı'] => ['Tahsil Yardımı'], ['Tahsil Yardımı Yüksekokul Birim Fiyat'];

['Aktif', 'Resmi Tatillerdeki Çalışma Gün Sayısı'] => ['Resmi Tatillerdeki Çalışma Ücreti'];

['Aktif', 'Çocuk Sayısı'] => ['Çocuk Yardımı'];

['Aktif', 'Kasa Tazminatı Alacak Kişi Sayısı'] => ['Kaza Tazminatı'];

['Aktif', 'İşten Ayrılan Kişi Sayısı'] => ['İşten Ayrılanların Senelik İzin Ödemesi'];
['Aktif', 'İşten Ayrılan Kişi Sayısı'] => ['İşten Ayrılanların Fazla Mesai Karş.'];

['Aktif', 'Flextra Kişi Sayısı'] => ['Flextra Şirket Katkı Payı'] ,['Flextra Şirket Katkı Payı Birim Fiyat'];

['Aktif', 'Tamamlayıcı Sağlık Sigortasından Yararlanan Kişi Sayısı'] => ['Tamamlayıcı Sağlık Sigortası'];

#IK_MM_V_P Feedarları.
['Aktif', 'Toplam Merkez Unvan', 'Çalışan_Alt_Grupları':'TOPLAM', 'Çalışan Sayısı'] => DB( 'IK_MM_V_P', !Masraf_Kar_Merkezleri, !Version, !Period, 'Çay Fişi'  );
['Aktif', 'Unvanlar':'TOPLAM', 'Çalışan_Alt_Grupları':'TOPLAM', 'Çalışan Sayısı'] => DB( 'IK_MM_V_P', !Masraf_Kar_Merkezleri, !Version, !Period, 'Yılbaşı Yemeği'  );

['Aktif', 'Çalışan Sayısı'] => DB( 'IK - Bordro', 'Toplam Maliyet', !Masraf_Kar_Merkezleri, !Unvanlar, !Çalışan_Alt_Grupları, !Version, !Period, 'Tutar');
['Aktif'] => DB( 'IK - Bordro', !İK_Genel_Giriş_Meas, !Masraf_Kar_Merkezleri, !Unvanlar, !Çalışan_Alt_Grupları, !Version, !Period, 'Tutar');
['Aktif', 'Full Time', 'EFT Sayısı'] => DB( 'IK - Bordro', 'Çalışan Sayısı', !Masraf_Kar_Merkezleri, !Unvanlar, !Çalışan_Alt_Grupları, !Version, !Period, 'Tutar EFT'  );


['Aktif', 'Genel Müdür', 'Full Time', 'Çalışan Sayısı'] => DB( 'IK - HC Rapor', !Masraf_Kar_Merkezleri, 'YÖNETİCİ', 'Toplam', !Version, !Period, 'Kişi Sayısı'  );

['Aktif', 'Genel Müdür Yardımcısı', 'Full Time', 'Çalışan Sayısı'] => DB( 'IK - HC Rapor', !Masraf_Kar_Merkezleri, 'YÖNETİCİ', 'Toplam', !Version, !Period, 'Kişi Sayısı'  );

['Aktif', 'TOPLAM UNVAN', 'TOPLAM ÇALIŞAN ALT GRUP', 'Çalışan Sayısı'] => DB( 'IK - HC Rapor', !Masraf_Kar_Merkezleri, 'TOPLAM', 'Toplam', !Version, !Period, 'Kişi Sayısı'  );

['Aktif', 'Mağaza Müdürü', 'Full Time', 'Çalışan Sayısı'] => DB( 'IK - HC Rapor', !Masraf_Kar_Merkezleri, 'YÖNETİCİ', 'Toplam', !Version, !Period, 'Kişi Sayısı'  );

['Aktif', 'Toplam Mağaza Unvan', 'TOPLAM ÇALIŞAN ALT GRUP', 'Çalışan Sayısı'] => DB( 'IK - HC Rapor', !Masraf_Kar_Merkezleri, 'AYLIK ÜCRETLİ', 'Toplam', !Version, !Period, 'Kişi Sayısı'  );

['Aktif', 'Müşteri Danışmanı', 'Part Time', 'Çalışan Sayısı'] => DB( 'IK - HC Rapor', !Masraf_Kar_Merkezleri, 'SAAT ÜCRETLİ', 'Toplam', !Version, !Period, 'Kişi Sayısı'  );

['Aktif', 'Müşteri Danışmanı', 'Sezonluk', 'Çalışan Sayısı'] => DB( 'IK - HC Rapor', !Masraf_Kar_Merkezleri, 'SEZONLUK', 'Toplam', !Version, !Period, 'Kişi Sayısı'  );

['Aktif', 'Unvanlar':'TOPLAM', 'Çalışan_Alt_Grupları':'TOPLAM', 'Çalışan Sayısı'] => DB( 'Idari Işler', 'Personel Sayısı', !Masraf_Kar_Merkezleri, !Version, !Period, 'Tutar'  );
