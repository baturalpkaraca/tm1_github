SKIPCHECK;

[Scenario:'Scenario':'Live',Alokasyon_M:'Alokasyon_M':{'Oran','FTE','M2'}]=N:
      IF( DB( 'Alokasyon', !Scenario, !Version, !Period, !Expense_Accounts, 'Eşit' )@='Evet', 0,CONTINUE );


[Scenario:'Scenario':'Live',Alokasyon_M:'Alokasyon_M':'Eşit (Pay)']=N:
            IF( ATTRN('Expense_Accounts',!Expense_Accounts,'Alokasyon')=1 & 
                  DB( 'Alokasyon', !Scenario, !Version, !Period, !Expense_Accounts, 'Eşit' )@='Evet',
                  DB( 'Alokasyon', !Scenario, !Version, !Period, !Expense_Accounts, 'Amount' ) \ (DIMSIZ( 'Cost_Center' )-6),
                  CONTINUE
            );
  
[Scenario:'Scenario':'Live',Alokasyon_M:'Alokasyon_M':'FTE (Pay)']=N:
            IF( ATTRN('Expense_Accounts',!Expense_Accounts,'Alokasyon')=1,
                  DB( 'Alokasyon', !Scenario, !Version, !Period, !Expense_Accounts, 'Amount' ) 
                * DB( 'Alokasyon', !Scenario, !Version, !Period, !Expense_Accounts, 'FTE' ) \100
                * [Alokasyon_M:'Alokasyon_M':'FTE'] \  [Alokasyon_M:'Alokasyon_M':'FTE',Cost_Center:'Cost_Center':'Total Cost Center'] ,
                CONTINUE
            );

[Scenario:'Scenario':'Live',Alokasyon_M:'Alokasyon_M':'FTE']=N:
            IF( ATTRN('Expense_Accounts',!Expense_Accounts,'Alokasyon')=1,
                DB('Salary_Cost_Center_Mapping', !Scenario, !Version, 'Entity':'Ereteam', !Period, 'Employees':'Total Employees', !Cost_Center, 'FTE' ),
              CONTINUE
            );

[Scenario:'Scenario':'Live',Alokasyon_M:'Alokasyon_M':'M2 (Pay)']=N:
            IF( ATTRN('Expense_Accounts',!Expense_Accounts,'Alokasyon')=1,
                   DB( 'Alokasyon', !Scenario, !Version, !Period, !Expense_Accounts, 'Amount' ) 
                * DB( 'Alokasyon', !Scenario, !Version, !Period, !Expense_Accounts, 'M2' ) \ 100
                *[Alokasyon_M:'Alokasyon_M':'M2']
                \ [Alokasyon_M:'Alokasyon_M':'M2',Cost_Center:'Cost_Center':'Total Cost Center'],
              CONTINUE
            );

[Scenario:'Scenario':'Live',Alokasyon_M:'Alokasyon_M':'Oran (Pay)']=N:
            IF( ATTRN('Expense_Accounts',!Expense_Accounts,'Alokasyon')=1,
                   DB( 'Alokasyon', !Scenario, !Version, !Period, !Expense_Accounts, 'Amount' ) 
                * DB( 'Alokasyon', !Scenario, !Version, !Period, !Expense_Accounts, 'Oran' ) \ 100
                *[Alokasyon_M:'Alokasyon_M':'Oran']
                \ [Alokasyon_M:'Alokasyon_M':'Oran',Cost_Center:'Cost_Center':'Total Cost Center'],
              CONTINUE
            );


[Scenario:'Scenario':'Live',Alokasyon_M:'Alokasyon_M':'M2']=N:
            IF( ATTRN('Expense_Accounts',!Expense_Accounts,'Alokasyon')=1,
                 ATTRN( 'Cost_Center', !Cost_Center, 'M2' ),
                CONTINUE
              );

[Scenario:'Scenario':'Live',Alokasyon_M:'Alokasyon_M':'Oran']=N:
            IF( ATTRN('Expense_Accounts',!Expense_Accounts,'Alokasyon')=1,
                 ATTRN( 'Cost_Center', !Cost_Center, 'Oran' ),
                CONTINUE
              );

FEEDERS;

  [Scenario:'Scenario':'Live',Alokasyon_M:'Alokasyon_M':'M2'] =>
      [Scenario:'Scenario':'Live',Alokasyon_M:'Alokasyon_M':'M2 (Pay)'] ;

  [Scenario:'Scenario':'Live',Alokasyon_M:'Alokasyon_M':'FTE'] =>
      [Scenario:'Scenario':'Live',Alokasyon_M:'Alokasyon_M':'FTE (Pay)'] ,
      [Scenario:'Scenario':'Live',Alokasyon_M:'Alokasyon_M':'Eşit (Pay)'] ;

  [Scenario:'Scenario':'Live',Alokasyon_M:'Alokasyon_M':'Oran']=>
      [Scenario:'Scenario':'Live',Alokasyon_M:'Alokasyon_M':'Oran (Pay)'];
  
[Scenario:'Scenario':'Live',Alokasyon_M:'Alokasyon_M':'Toplam Tutar']=>
      DB( 'Expense', !Scenario, !Version, 'xyz Bank', !Period, !Cost_Center, !Expense_Accounts, 'TRY', 'TRY', 'Amount' );


