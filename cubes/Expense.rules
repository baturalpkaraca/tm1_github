SKIPCHECK;
FEEDSTRINGS;
# [Cost_Center:'Cost_Center':'FPM Services']=STET;
[Scenario:'Scenario':'Live']=N:
            IF(DB( 'System_Version', !Scenario, !Version, !Entity, !Period, 'Input' )@='Actual',
                [Scenario:'Scenario':'Actual'],
                CONTINUE
            );

[Scenario:'Scenario':'Live',Currency_Reporting:'Currency_Reporting':'TRY',Currency:'Currency':'TRY'] =N:
      IF( ATTRN( 'Expense_Accounts', !Expense_Accounts, 'Alokasyon' )=1,
          DB( 'Alokasyon_Cost_Center', !Scenario, !Version, !Period, !Cost_Center, !Expense_Accounts, 'Alokasyon_M':'Toplam Tutar' ),
          CONTINUE
      );

[Scenario:'Scenario':'Live',Currency_Reporting:'Currency_Reporting':'TRY',Currency:'Currency':'TRY'] =N:
      IF(ATTRS('Expense_Accounts',!Expense_Accounts,'Benefits')@<>'' & ATTRN( 'Expense_Accounts', !Expense_Accounts, 'Daily' )=0,
             DB('Benefits', !Scenario, !Version, !Entity, !Period,  ATTRS('Expense_Accounts',!Expense_Accounts,'Benefits'))
            * [Expense_Accounts:'Expense_Accounts':'FTE'],
          CONTINUE  
      ); 

[Scenario:'Scenario':'Live',Currency_Reporting:'Currency_Reporting':'TRY',Currency:'Currency':'TRY'] =N:
      IF(ATTRS('Expense_Accounts',!Expense_Accounts,'Benefits')@<>'',
            DB('Benefits', !Scenario, !Version, !Entity, !Period,  ATTRS('Expense_Accounts',!Expense_Accounts,'Benefits'))
            * DB( 'Benefits', !Scenario, !Version, !Entity, !Period, 'Business Days' )
            * [Expense_Accounts:'Expense_Accounts':'FTE'],
          CONTINUE  
      );

[Scenario:'Scenario':'Live',Currency_Reporting:'Currency_Reporting':'TRY',Currency:'Currency':'TRY'] =N:
      IF(ATTRS('Expense_Accounts',!Expense_Accounts,'Payroll')@<>'',
          DB('Salary_Cost_Center_Mapping', !Scenario, !Version, !Entity, !Period, 'Employees':'Total Employees', !Cost_Center, ATTRS('Expense_Accounts',!Expense_Accounts,'Payroll') ),
          CONTINUE  
      );

[Scenario:'Scenario':'Live',Currency_Reporting:'Currency_Reporting':'TRY',Currency:'Currency':'TRY'] =N:
      IF( ATTRN( 'Expense_Accounts', !Expense_Accounts, 'Alokasyon' )=0,
            DB( 'Expense_Entry', !Scenario, !Version, !Entity, SUBST( !Period, 5, 4 ), !Cost_Center, !Expense_Accounts, 'Items':'Total_Items', SUBST( !Period, 1, 3 ) | 'TRY' ),
            CONTINUE
      );

[Scenario:'Scenario':'Live'] =N:
            IF(!Currency@='TRY' & !Currency_Reporting@<>'TRY' & !Expense_Accounts@<>'FTE', 
                [Currency_Reporting:'Currency_Reporting':'TRY',Currency:'Currency':'TRY'] \ 
                DB( 'Macoeconomic_Indicators', !Scenario, !Version, !Entity, !Period, !Currency_Reporting ),
                CONTINUE  
            );

[Scenario:'Scenario':'Live',Expense_Accounts:'Expense_Accounts':'FTE'] =N:
            IF(!Currency@='TRY' & !Currency_Reporting@<>'TRY' ,
                [Currency_Reporting:'Currency_Reporting':'TRY'],
                CONTINUE
           );

[Scenario:'Scenario':'Live',Expense_M:'Expense_M':'FTE'] =
        DB('Salary_Cost_Center_Mapping', !Scenario, !Version, !Entity, !Period, 'Employees':'Total Employees', !Cost_Center, 'FTE' );

[Expense_Accounts:'Expense_Accounts':'FTE'] =
    IF(DTYPE( 'Period', !Period )@='C',
        DB( 'Expense', !Scenario, !Version, !Entity, ATTRS('Period',!Period,'Last Period'), !Cost_Center, !Expense_Accounts, !Currency, !Currency_Reporting, !Expense_M ),
        CONTINUE
  );
    

FEEDERS;

[Scenario:'Scenario':'Live',Currency_Reporting:'Currency_Reporting':'TRY',Currency:'Currency':'TRY'] =>
        [Currency:'TRY',Currency_Reporting:'EUR'], 
        [Currency:'TRY',Currency_Reporting:'USD'];

[Scenario:'Scenario':'Live',Currency_Reporting:'Currency_Reporting':'TRY',Currency:'Currency':'TRY',Expense_Accounts:'Expense_Accounts':'Brüt Ücret'] =>
        [Expense_Accounts:'Expense_Accounts':'FTE'];