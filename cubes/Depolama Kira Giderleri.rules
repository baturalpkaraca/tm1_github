SKIPCHECK;

[  ] = IF(ELPAR('Version', !Version, 1) @= 'Aktif', CONTINUE, STET);

[ { 'Kapanış Tutarı', 'Oran %', 'Kira Ciro %', 'Artış Tutarı (Sabit için)' } ] =  C: 0;

[ { 'Para Birimi', 'Sabit/Oran', 'Oran Seçimi', 'Manuel Oran Açıklaması', 'Değişim Ayı' } ] =  S: 
	IF(DTYPE('Period', !Period) @= 'C'
	% DTYPE('Masraf_Kar_Merkezleri', !Masraf_Kar_Merkezleri) @= 'C', '', CONTINUE);

[ 'Değişim ayı' ] =  S: 
	IF(SUBST(DB('MM_V', !Masraf_Kar_Merkezleri, !Version, 'Açılış Tarihi (yyyy-mm-dd)'), 1, 4) @= SUBST(!Period, 1, 4), '', CONTINUE);

# Artış Tutarı 0 olmalı
[ 'Artış Tutarı (Sabit için)' ] =  N: 
	IF(SUBST(DB('MM_V', !Masraf_Kar_Merkezleri, !Version, 'Açılış Tarihi (yyyy-mm-dd)'), 1, 4) @= SUBST(!Period, 1, 4), 0, CONTINUE);

[ 'Oran Seçimi' ] =  S: 
	IF(DB('Depolama Kira Giderleri', !Masraf_Kar_Merkezleri, !Version, !Period, 'Sabit/Oran') @= 'Sabit', '', CONTINUE);

[ 'Oran %' ] =  N: 
	IF(DB('Depolama Kira Giderleri', !Masraf_Kar_Merkezleri, !Version, !Period, 'Sabit/Oran') @= 'Sabit', 0, CONTINUE);

[ 'Oran %' ] =  N: 
	IF(DB('Depolama Kira Giderleri', !Masraf_Kar_Merkezleri, !Version, !Period, 'Oran Seçimi') @= 'TÜFE', 
    DB('V_P', !Version, ATTRS('Period', DB('Depolama Kira Giderleri', !Masraf_Kar_Merkezleri, !Version, !Period, 'Değişim ayı'), 'PrevMonth'), 'Enflasyon Oranı % - TÜFE'), CONTINUE);

#TÜFE - 12 Aylık Geliştirmesi.
[ 'Oran %' ] =  N: 
	IF(DB('Depolama Kira Giderleri', !Masraf_Kar_Merkezleri, !Version, !Period, 'Oran Seçimi') @= 'TÜFE - 12 Aylık', 
    DB('V_P', !Version, ATTRS('Period', DB('Depolama Kira Giderleri', !Masraf_Kar_Merkezleri, !Version, !Period, 'Değişim ayı'), 'PrevMonth'), 'Enflasyon Oranı % - TÜFE - 12 Aylık'), CONTINUE);

[ 'Oran %' ] =  N: 
	IF(DB('Depolama Kira Giderleri', !Masraf_Kar_Merkezleri, !Version, !Period, 'Oran Seçimi') @= 'ÜFE', 
    DB('V_P', !Version, ATTRS('Period', DB('Depolama Kira Giderleri', !Masraf_Kar_Merkezleri, !Version, !Period, 'Değişim ayı'), 'PrevMonth'), 'Enflasyon Oranı % - ÜFE'), CONTINUE);

[ 'Oran %' ] =  N: 
	IF(DB('Depolama Kira Giderleri', !Masraf_Kar_Merkezleri, !Version, !Period, 'Oran Seçimi') @= 'ÜFE TÜFE ort.', 
    DB('V_P', !Version, ATTRS('Period', DB('Depolama Kira Giderleri', !Masraf_Kar_Merkezleri, !Version, !Period, 'Değişim ayı'), 'PrevMonth'), 'Enflasyon Oranı % - ÜFE TÜFE ort.'), CONTINUE);

[ 'Artış Tutarı (Sabit için)' ] =  N: 
	IF(DB('Depolama Kira Giderleri', !Masraf_Kar_Merkezleri, !Version, !Period, 'Sabit/Oran') @= 'Oran', 0, CONTINUE);

# Değişim ayı boş bırakılmış ise
[ 'Hesaplanan Kira' ] =  N: 
	IF(DB( 'Depolama Kira Giderleri', !Masraf_Kar_Merkezleri, !Version, !Period, 'Değişim ayı'  ) @= '', 
    DB('Depolama Kira Giderleri', !Masraf_Kar_Merkezleri, !Version, !Period, 'Kapanış Tutarı'), CONTINUE);

# Değişim ayından önceki aylar için
[ 'Hesaplanan Kira' ] =  N: IF(ATTRN('Period', !Period, 'Index') < 
    ATTRN('Period', DB('Depolama Kira Giderleri', !Masraf_Kar_Merkezleri, !Version, !Period, 'Değişim ayı'), 'Index'), 
    DB('Depolama Kira Giderleri', !Masraf_Kar_Merkezleri, !Version, !Period, 'Kapanış Tutarı'), CONTINUE);

# Değişim ayından sonraki aylar için
[ 'Hesaplanan Kira' ] =  N: 
	IF(ATTRN('Period', !Period, 'Index') >= 
    ATTRN('Period', DB('Depolama Kira Giderleri', !Masraf_Kar_Merkezleri, !Version, !Period, 'Değişim ayı'), 'Index'), 
    IF(DB('Depolama Kira Giderleri', !Masraf_Kar_Merkezleri, !Version, !Period, 'Sabit/Oran') @= 'Oran', 
    DB('Depolama Kira Giderleri', !Masraf_Kar_Merkezleri, !Version, !Period, 'Kapanış Tutarı') * 
    (DB('Depolama Kira Giderleri', !Masraf_Kar_Merkezleri, !Version, !Period, 'Oran %') + 1), 
    DB('Depolama Kira Giderleri', !Masraf_Kar_Merkezleri, !Version, !Period, 'Kapanış Tutarı') + 
    DB('Depolama Kira Giderleri', !Masraf_Kar_Merkezleri, !Version, !Period, 'Artış Tutarı (Sabit için)')), CONTINUE);

# Yabancı para birimleri için
[ 'Hesaplanan Kira (TL)' ] =  N: 
	IF(DB('Depolama Kira Giderleri', !Masraf_Kar_Merkezleri, !Version, !Period, 'Para Birimi') @= 'USD', 
    [ 'Hesaplanan Kira' ] * DB('V_P', !Version, !Period, 'Aysonu Kur - USD'), CONTINUE);

[ 'Hesaplanan Kira (TL)' ] =  N: 
	IF(DB('Depolama Kira Giderleri', !Masraf_Kar_Merkezleri, !Version, !Period, 'Para Birimi') @= 'EUR', 
    [ 'Hesaplanan Kira' ] * DB('V_P', !Version, !Period, 'Aysonu Kur - EUR'), CONTINUE);

[ 'Hesaplanan Kira (TL)' ] =  N: 
	IF(DB('Depolama Kira Giderleri', !Masraf_Kar_Merkezleri, !Version, !Period, 'Para Birimi') @= 'GBP', 
    [ 'Hesaplanan Kira' ] * DB('V_P', !Version, !Period, 'Aysonu Kur - GBP'), CONTINUE);

# Para birimi seçimi yapılmamış ise TL gibi hesaplanacak
[ 'Hesaplanan Kira (TL)' ] =  N: [ 'Hesaplanan Kira' ];

[ 'Hesaplanan Kira (TRY)' ] =  N: 
	IF(DB('Depolama Kira Giderleri', !Masraf_Kar_Merkezleri, !Version, !Period, 'Para Birimi') @= 'TL'
	%  DB('Depolama Kira Giderleri', !Masraf_Kar_Merkezleri, !Version, !Period, 'Para Birimi') @= '', 
    [ 'Hesaplanan Kira' ], 0);

[ 'Hesaplanan Kira (USD)' ] =  N: 
	IF(DB('Depolama Kira Giderleri', !Masraf_Kar_Merkezleri, !Version, !Period, 'Para Birimi') @= 'USD', 
    [ 'Hesaplanan Kira' ], 0);

[ 'Hesaplanan Kira (EUR)' ] =  N: 
	IF(DB('Depolama Kira Giderleri', !Masraf_Kar_Merkezleri, !Version, !Period, 'Para Birimi') @= 'EUR', 
    [ 'Hesaplanan Kira' ], 0);

[ 'Hesaplanan Kira (GBP)' ] =  N: 
	IF(DB('Depolama Kira Giderleri', !Masraf_Kar_Merkezleri, !Version, !Period, 'Para Birimi') @= 'GBP', 
    [ 'Hesaplanan Kira' ], 0);

FEEDERS;

[ 'Aktif', 'Kapanış Tutarı' ] => [ 'Hesaplanan Kira' ], [ 'Hesaplanan Kira (TL)' ], [ 'Hesaplanan Kira (TRY)' ], [ 'Hesaplanan Kira (USD)' ], [ 'Hesaplanan Kira (EUR)' ], [ 'Hesaplanan Kira (GBP)' ];

['Aktif', '5340000', 'Hesaplanan Kira (TL)'] => DB( 'Depolama Hesaplama', 'Kira Giderleri', !Version, !Period, 'Toplam 5001'  );

['Aktif', {'3340002', '3340010', '3340015', '3350000', '3340007', '3340008', '5340300', '1345000'}, 'Hesaplanan Kira (TL)'] => 
DB( 'Depolama Hesaplama Mağaza Dağıtım', !Masraf_Kar_Merkezleri, 'Kira Giderleri', !Version, !Period, 'Amount'  );
