FEEDSTRINGS;

SKIPCHECK;

[  ] = IF(ELPAR('Version', !Version, 1) @= 'Aktif', CONTINUE, STET);

[ 'Tutar' ] =  N: 
	IF(SUBST(!Period, 1, 7) @= 'Opening', STET, CONTINUE);

[ 'Tutar' ] =  N: 
	IF(SUBST(DB('Kredi Faiz', !Records, !Version, !Period, 'Kapanış (yyyy-mm-dd)'), 1, 7) @= !Period, 0, CONTINUE);

[ 'Tutar' ] =  N: 
	IF(DAYNO(DB('Kredi Faiz', !Records, !Version, 'Opening ' | ATTRS('Period', !Period, 'Year'), 'Açılış (yyyy-mm-dd)')) > DAYNO(ATTRS('Period', !Period, 'LastDay')), 0, CONTINUE);

[ 'Tutar' ] =  N: 
	IF(SUBST(DB('Kredi Faiz', !Records, !Version, !Period, 'Kapanış (yyyy-mm-dd)'), 1, 7) @> !Period, DB('Kredi Faiz', !Records, !Version, 'Opening ' | ATTRS('Period', !Period, 'Year'), 'Tutar'), 0);

[ 'Güncel Faiz Oranı' ] =  N: 
	IF(DB('Kredi Faiz', !Records, !Version, !Period, 'Faiz Değişim Ayı') @= 'Evet'
	% SUBST(!Period, 1, 7) @= 'Opening', STET, CONTINUE);

[ 'Güncel Faiz Oranı' ] =  N: 
	IF(SUBST(!Period, 6, 2) @= '01', DB('Kredi Faiz', !Records, !Version, 'Opening ' | ATTRS('Period', !Period, 'Year'), 'Güncel Faiz Oranı'), CONTINUE);

[ 'Güncel Faiz Oranı' ] =  N: 
	DB('Kredi Faiz', !Records, !Version, ATTRS('Period', !Period, 'PrevMonth'), 'Güncel Faiz Oranı');

[ { 'Banka Adı', 'Kredi Türü', 'Açılış (yyyy-mm-dd)', 'Kapanış (yyyy-mm-dd)', 'Ödeme Periyodu' } ] =  S: 
	IF(SUBST(!Period, 1, 7) @<> 'Opening', DB('Kredi Faiz', !Records, !Version, 'Opening ' | ATTRS('Period', !Period, 'Year'), !Kredi_Faiz_Meas), CONTINUE);

[ 'BSMV Oran' ] =  N: 
	IF(SUBST(!Period, 1, 7) @<> 'Opening', DB('Kredi Faiz', !Records, !Version, 'Opening ' | ATTRS('Period', !Period, 'Year'), !Kredi_Faiz_Meas), CONTINUE);

[ 'Tutar (Calc)' ] =  N: 
	IF(!Period @= SUBST(DB('Kredi Faiz', !Records, !Version, 'Opening ' | ATTRS('Period', !Period, 'Year'), 'Açılış (yyyy-mm-dd)'), 1, 7), DB('Kredi Faiz', !Records, !Version, !Period, 'Tutar'), 0);

[ 'Kısa Fin Ödeme', 'Total Records' ] =  C: 
	IF(SUBST(!Period, 6, 2) @= '03'
	% SUBST(!Period, 6, 2) @= '06'
	% SUBST(!Period, 6, 2) @= '09'
	% SUBST(!Period, 6, 2) @= '12', [ 'Kısa Fin Karşılık' ] + DB('Kredi Faiz', !Records, !Version, STR(ATTRN('Period', !Period, 'Index')-1, 2, 0), 'Kısa Fin Karşılık') + DB('Kredi Faiz', !Records, !Version, STR(ATTRN('Period', !Period, 'Index')-2, 2, 0), 'Kısa Fin Karşılık'), 0);

# Opening döneminde sıfırlanacak alanlar.

[ { 'Uzun Fin Karşılık', 'Uzun Fin Ödeme', 'Kısa Fin Karşılık', 'Kısa Fin Ödeme' } ] =  N: 
	IF(SUBST(!Period, 1, 7) @= 'Opening', 0, CONTINUE);

[ 'Gün' ] =  N: 
	IF(DB('Kredi Faiz', !Records, !Version, !Period, 'Kapanış (yyyy-mm-dd)') @<> ''
	& DB('Kredi Faiz', !Records, !Version, !Period, 'Açılış (yyyy-mm-dd)') @<> '', DAYNO(DB('Kredi Faiz', !Records, !Version, !Period, 'Kapanış (yyyy-mm-dd)')) - DAYNO(DB('Kredi Faiz', !Records, !Version, !Period, 'Açılış (yyyy-mm-dd)')), CONTINUE);

[ 'Güncel T. Faiz' ] =  N: 
	IF(DB('Kredi Faiz', !Records, !Version, !Period, 'Kredi Türü') @= 'Tahvil', IF(DAYNO(DB('Kredi Faiz', !Records, !Version, 'Opening ' | ATTRS('Period', !Period, 'Year'), 'Kapanış (yyyy-mm-dd)')) <= DAYNO(ATTRS('Period', !Period, 'LastDay'))
	& DAYNO(DB('Kredi Faiz', !Records, !Version, 'Opening ' | ATTRS('Period', !Period, 'Year'), 'Kapanış (yyyy-mm-dd)')) >= DAYNO(ATTRS('Period', !Period, 'FirstDay'))
	& [ 'Güncel Faiz Oranı' ] <> 0, (DB('Kredi Faiz', !Records, !Version, 'Opening ' | ATTRS('Period', !Period, 'Year'), 'Tutar') * [ 'Güncel Faiz Oranı' ] * [ 'Gün' ] \ 365) * (1 + [ 'BSMV Oran' ]), CONTINUE), CONTINUE);

[ 'Güncel T. Faiz' ] =  N: 
	IF(DB('Kredi Faiz', !Records, !Version, !Period, 'Kredi Türü') @= 'Tahvil', IF([ 'Tutar' ] <> 0
	& [ 'Güncel Faiz Oranı' ] <> 0, ([ 'Tutar' ] * [ 'Güncel Faiz Oranı' ] * [ 'Gün' ] \ 365) * (1 + [ 'BSMV Oran' ]), CONTINUE), CONTINUE);

[ 'Güncel T. Faiz' ] =  N: 
	IF(DB('Kredi Faiz', !Records, !Version, !Period, 'Kredi Türü') @= 'Kredi', IF(DAYNO(DB('Kredi Faiz', !Records, !Version, 'Opening ' | ATTRS('Period', !Period, 'Year'), 'Kapanış (yyyy-mm-dd)')) <= DAYNO(ATTRS('Period', !Period, 'LastDay'))
	& DAYNO(DB('Kredi Faiz', !Records, !Version, 'Opening ' | ATTRS('Period', !Period, 'Year'), 'Kapanış (yyyy-mm-dd)')) >= DAYNO(ATTRS('Period', !Period, 'FirstDay'))
	& [ 'Güncel Faiz Oranı' ] <> 0, (DB('Kredi Faiz', !Records, !Version, 'Opening ' | ATTRS('Period', !Period, 'Year'), 'Tutar') * [ 'Güncel Faiz Oranı' ] * [ 'Gün' ] \ 360) * (1 + [ 'BSMV Oran' ]), CONTINUE), CONTINUE);

[ 'Güncel T. Faiz' ] =  N: 
	IF(DB('Kredi Faiz', !Records, !Version, !Period, 'Kredi Türü') @= 'Kredi', IF([ 'Tutar' ] <> 0
	& [ 'Güncel Faiz Oranı' ] <> 0, ([ 'Tutar' ] * [ 'Güncel Faiz Oranı' ] * [ 'Gün' ] \ 360) * (1 + [ 'BSMV Oran' ]), CONTINUE), CONTINUE);

# Kredi başlamadan önceki bütçe aylarında ödeme yapılmamalı.

[ 'Ödeme Tarihi' ] =  S: 
	IF(DAYNO(DB('Kredi Faiz', !Records, !Version, 'Opening ' | ATTRS('Period', !Period, 'Year'), 'Açılış (yyyy-mm-dd)')) > DAYNO(ATTRS('Period', !Period, 'LastDay')), '', CONTINUE);

# Kredinin başladığı ayda ise ödeme yapılmamalı.

[ 'Ödeme Tarihi' ] =  S: 
	IF(DAYNO(DB('Kredi Faiz', !Records, !Version, 'Opening ' | ATTRS('Period', !Period, 'Year'), 'Açılış (yyyy-mm-dd)')) >= DAYNO(ATTRS('Period', !Period, 'FirstDay'))
	& DAYNO(DB('Kredi Faiz', !Records, !Version, 'Opening ' | ATTRS('Period', !Period, 'Year'), 'Açılış (yyyy-mm-dd)')) <= DAYNO(ATTRS('Period', !Period, 'LastDay')), '', CONTINUE);

# Kredi kapandıktan sonra ödeme yapılmamalı.

[ 'Ödeme Tarihi' ] =  S: 
	IF(DAYNO(DB('Kredi Faiz', !Records, !Version, 'Opening ' | ATTRS('Period', !Period, 'Year'), 'Kapanış (yyyy-mm-dd)')) < DAYNO(ATTRS('Period', !Period, 'FirstDay')), '', CONTINUE);

# Dönem sonu ödemeler.

[ 'Ödeme Tarihi' ] =  S: 
	IF(DB('Kredi Faiz', !Records, !Version, 'Opening ' | ATTRS('Period', !Period, 'Year'), 'Ödeme Periyodu') @= 'Dönem Sonu'
	& SUBST(!Period, 6, 2) @= '12', !Period | '-' | STR(DAY(DB('Kredi Faiz', !Records, !Version, 'Opening ' | ATTRS('Period', !Period, 'Year'), 'Açılış (yyyy-mm-dd)')), 2, 0), CONTINUE);

# Yıllık ödemeler.

[ 'Ödeme Tarihi' ] =  S: 
	IF(DB('Kredi Faiz', !Records, !Version, 'Opening ' | ATTRS('Period', !Period, 'Year'), 'Ödeme Periyodu') @= 'Yıllık'
	& MONTH(DB('Kredi Faiz', !Records, !Version, 'Opening ' | ATTRS('Period', !Period, 'Year'), 'Açılış (yyyy-mm-dd)')) = NUMBR(SUBST(!Period, 6, 2)), !Period | '-' | STR(DAY(DB('Kredi Faiz', !Records, !Version, 'Opening ' | ATTRS('Period', !Period, 'Year'), 'Açılış (yyyy-mm-dd)')), 2, 0), CONTINUE);

# 6 Aylık ödemeler.

[ 'Ödeme Tarihi' ] =  S: 
	IF(DB('Kredi Faiz', !Records, !Version, 'Opening '| ATTRS( 'Period', !Period, 'Year' ), 'Ödeme Periyodu') @= '6 Aylık'
	& (MONTH(DB('Kredi Faiz', !Records, !Version, 'Opening '| ATTRS( 'Period', !Period, 'Year' ), 'Açılış (yyyy-mm-dd)')) = NUMBR(SUBST(!Period, 6, 2))
	% IF(MOD(MONTH(DB('Kredi Faiz', !Records, !Version, 'Opening '| ATTRS( 'Period', !Period, 'Year' ), 'Açılış (yyyy-mm-dd)')) + 6, 12) = 0, 12, MOD(MONTH(DB('Kredi Faiz', !Records, !Version, 'Opening '| ATTRS( 'Period', !Period, 'Year' ), 'Açılış (yyyy-mm-dd)')) + 6, 12)) = NUMBR(SUBST(!Period, 6, 2))), !Period | '-' | STR(DAY(DB('Kredi Faiz', !Records, !Version, 'Opening '| ATTRS( 'Period', !Period, 'Year' ), 'Açılış (yyyy-mm-dd)')), 2, 0), CONTINUE);

# 3 Aylık ödemeler.

[ 'Ödeme Tarihi' ] =  S: 
	IF(DB('Kredi Faiz', !Records, !Version, 'Opening '| ATTRS( 'Period', !Period, 'Year' ), 'Ödeme Periyodu') @= '3 Aylık'
	& (MONTH(DB('Kredi Faiz', !Records, !Version, 'Opening '| ATTRS( 'Period', !Period, 'Year' ), 'Açılış (yyyy-mm-dd)')) = NUMBR(SUBST(!Period, 6, 2))
	% IF(MOD(MONTH(DB('Kredi Faiz', !Records, !Version, 'Opening '| ATTRS( 'Period', !Period, 'Year' ), 'Açılış (yyyy-mm-dd)')) + 3, 12) = 0, 12, MOD(MONTH(DB('Kredi Faiz', !Records, !Version, 'Opening '| ATTRS( 'Period', !Period, 'Year' ), 'Açılış (yyyy-mm-dd)')) + 3, 12)) = NUMBR(SUBST(!Period, 6, 2))
	% IF(MOD(MONTH(DB('Kredi Faiz', !Records, !Version, 'Opening '| ATTRS( 'Period', !Period, 'Year' ), 'Açılış (yyyy-mm-dd)')) + 6, 12) = 0, 12, MOD(MONTH(DB('Kredi Faiz', !Records, !Version, 'Opening '| ATTRS( 'Period', !Period, 'Year' ), 'Açılış (yyyy-mm-dd)')) + 6, 12)) = NUMBR(SUBST(!Period, 6, 2))
	% IF(MOD(MONTH(DB('Kredi Faiz', !Records, !Version, 'Opening '| ATTRS( 'Period', !Period, 'Year' ), 'Açılış (yyyy-mm-dd)')) + 9, 12) = 0, 12, MOD(MONTH(DB('Kredi Faiz', !Records, !Version, 'Opening '| ATTRS( 'Period', !Period, 'Year' ), 'Açılış (yyyy-mm-dd)')) + 9, 12)) = NUMBR(SUBST(!Period, 6, 2))), !Period | '-' | STR(DAY(DB('Kredi Faiz', !Records, !Version, 'Opening '| ATTRS( 'Period', !Period, 'Year' ), 'Açılış (yyyy-mm-dd)')), 2, 0), CONTINUE);

# Aylık ödemeler.

[ 'Ödeme Tarihi' ] =  S: 
	IF(DB('Kredi Faiz', !Records, !Version, 'Opening '| ATTRS( 'Period', !Period, 'Year' ), 'Ödeme Periyodu') @= 'Aylık', !Period | '-' | STR(DAY(DB('Kredi Faiz', !Records, !Version, 'Opening '| ATTRS( 'Period', !Period, 'Year' ), 'Açılış (yyyy-mm-dd)')), 2, 0), CONTINUE);

# Ödeme periyodundan önce kredi kapandı ise ödeme tarihi yaz.

[ 'Ödeme Tarihi' ] =  S: 
	IF(DB('Kredi Faiz', !Records, ATTRS('Period', !Period, 'NextMonth'), !Version, 'Tutar') = 0
	& DAYNO(DB('Kredi Faiz', !Records, !Version, 'Opening '| ATTRS( 'Period', !Period, 'Year' ), 'Kapanış (yyyy-mm-dd)')) <= DAYNO(ATTRS('Period', !Period, 'LastDay'))
	& DAYNO(DB('Kredi Faiz', !Records, !Version, 'Opening '| ATTRS( 'Period', !Period, 'Year' ), 'Kapanış (yyyy-mm-dd)')) >= DAYNO(ATTRS('Period', !Period, 'FirstDay')), !Period | '-' | STR(DAY(DB('Kredi Faiz', !Records, !Version, 'Opening '| ATTRS( 'Period', !Period, 'Year' ), 'Açılış (yyyy-mm-dd)')), 2, 0), CONTINUE);

[ 'Ödeme Tarihi' ] =  S: 
	'';

[ 'Bir Önceki Ödeme Tarihi' ] =  S: 
	IF(SUBST(!Period, 6, 2) @= '01'
	& DAYNO(ATTRS('Period', !Period, 'FirstDay')) <= DAYNO(DB('Kredi Faiz', !Records, !Version, 'Opening '| ATTRS( 'Period', !Period, 'Year' ), 'Açılış (yyyy-mm-dd)')), '', CONTINUE);

# 3 Aylık..

[ 'Bir Önceki Ödeme Tarihi' ] =  S: 
	IF(SUBST(!Period, 6, 2) @= '01'
	& DB('Kredi Faiz', !Records, !Version, ATTRS('Period', !Period, 'PrevMonth'), 'Ödeme Tarihi') @= ''
	& DB('Kredi Faiz', !Records, !Version, 'Opening '| ATTRS( 'Period', !Period, 'Year' ), 'Ödeme Periyodu') @= '3 Aylık', ATTRS('Period', STR((ATTRN('Period', !Period, 'Index') - 3 - 1 + IF(MOD(MONTH(DB('Kredi Faiz', !Records, !Version, 'Opening '| ATTRS( 'Period', !Period, 'Year' ), 'Açılış (yyyy-mm-dd)')), 3) = 0, 3, MOD(MONTH(DB('Kredi Faiz', !Records, !Version, 'Opening '| ATTRS( 'Period', !Period, 'Year' ), 'Açılış (yyyy-mm-dd)')), 3))), 3, 0), 'PeriodID') | '-' | SUBST(DB('Kredi Faiz', !Records, !Version, 'Opening '| ATTRS( 'Period', !Period, 'Year' ), 'Açılış (yyyy-mm-dd)'), 9, 2), CONTINUE);

# 6 Aylık..

[ 'Bir Önceki Ödeme Tarihi' ] =  S: 
	IF(SUBST(!Period, 6, 2) @= '01'
	& DB('Kredi Faiz', !Records, !Version, ATTRS('Period', !Period, 'PrevMonth'), 'Ödeme Tarihi') @= ''
	& DB('Kredi Faiz', !Records, !Version, 'Opening '| ATTRS( 'Period', !Period, 'Year' ), 'Ödeme Periyodu') @= '6 Aylık', ATTRS('Period', STR((ATTRN('Period', !Period, 'Index') - 6 - 1 + IF(MOD(MONTH(DB('Kredi Faiz', !Records, !Version, 'Opening '| ATTRS( 'Period', !Period, 'Year' ), 'Açılış (yyyy-mm-dd)')), 6) = 0, 6, MOD(MONTH(DB('Kredi Faiz', !Records, !Version, 'Opening '| ATTRS( 'Period', !Period, 'Year' ), 'Açılış (yyyy-mm-dd)')), 6))), 3, 0), 'PeriodID') | '-' | SUBST(DB('Kredi Faiz', !Records, !Version, 'Opening '| ATTRS( 'Period', !Period, 'Year' ), 'Açılış (yyyy-mm-dd)'), 9, 2), CONTINUE);

[ 'Bir Önceki Ödeme Tarihi' ] =  S: 
	IF(DB('Kredi Faiz', !Records, !Version, ATTRS('Period', !Period, 'PrevMonth'), 'Ödeme Tarihi') @<> '', DB('Kredi Faiz', !Records, !Version, ATTRS('Period', !Period, 'PrevMonth'), 'Ödeme Tarihi'), DB('Kredi Faiz', !Records, !Version, ATTRS('Period', !Period, 'PrevMonth'), 'Bir Önceki Ödeme Tarihi'));

[ 'Ayrılan Karşılık' ] =  N: 
	IF(SUBST(!Period, 1, 7) @= 'Opening', 0, CONTINUE);

# Kredi başlamadan önceki bütçe aylarında 0 olmalı.

[ 'Ayrılan Karşılık' ] =  N: 
	IF(DAYNO(DB('Kredi Faiz', !Records, !Version, 'Opening '| ATTRS( 'Period', !Period, 'Year' ), 'Açılış (yyyy-mm-dd)')) > DAYNO(ATTRS('Period', !Period, 'LastDay')), 0, CONTINUE);

# Kredinin başladığı ayda ayın sonuna kadarki gün sayısı kadar hesaplanmalı.

[ 'Ayrılan Karşılık' ] =  N: 
	IF(DAYNO(DB('Kredi Faiz', !Records, !Version, 'Opening '| ATTRS( 'Period', !Period, 'Year' ), 'Açılış (yyyy-mm-dd)')) > DAYNO(ATTRS('Period', !Period, 'FirstDay')), DB('Kredi Faiz', !Records, !Version, !Period, 'Güncel T. Faiz') * (ATTRN('Period', !Period, 'DaysNumber') - DAY(DB('Kredi Faiz', !Records, !Version, 'Opening '| ATTRS( 'Period', !Period, 'Year' ), 'Açılış (yyyy-mm-dd)')) + 1) \ DB('Kredi Faiz', !Records, !Version, 'Opening '| ATTRS( 'Period', !Period, 'Year' ), 'Gün'), CONTINUE);

# Kredi kapandığında sıfır olmalı.

[ 'Ayrılan Karşılık' ] =  N: 
	IF(DAYNO(DB('Kredi Faiz', !Records, !Version, 'Opening '| ATTRS( 'Period', !Period, 'Year' ), 'Kapanış (yyyy-mm-dd)')) < DAYNO(ATTRS('Period', !Period, 'FirstDay')), 0, CONTINUE);

# Kredi kapandığı ayda.

[ 'Ayrılan Karşılık' ] =  N: 
	IF(DAYNO(DB('Kredi Faiz', !Records, !Version, 'Opening '| ATTRS( 'Period', !Period, 'Year' ), 'Kapanış (yyyy-mm-dd)')) <= DAYNO(ATTRS('Period', !Period, 'LastDay'))
	& DAYNO(DB('Kredi Faiz', !Records, !Version, 'Opening '| ATTRS( 'Period', !Period, 'Year' ), 'Kapanış (yyyy-mm-dd)')) >= DAYNO(ATTRS('Period', !Period, 'FirstDay')), DB('Kredi Faiz', !Records, !Version, !Period, 'Güncel T. Faiz') * (DAY(DB('Kredi Faiz', !Records, !Version, 'Opening '| ATTRS( 'Period', !Period, 'Year' ), 'Kapanış (yyyy-mm-dd)'))) \ DB('Kredi Faiz', !Records, !Version, 'Opening '| ATTRS( 'Period', !Period, 'Year' ), 'Gün'), CONTINUE);

# Genel formül.

[ 'Ayrılan Karşılık' ] =  N: 
	DB('Kredi Faiz', !Records, !Version, !Period, 'Güncel T. Faiz') * (ATTRN('Period', !Period, 'DaysNumber')) \ DB('Kredi Faiz', !Records, !Version, 'Opening '| ATTRS( 'Period', !Period, 'Year' ), 'Gün');

# Giriş dönemlerinde STET.

[ 'Tutar' ] =  N: 
	IF(SUBST(!Period, 1, 7) @= 'Opening', STET, CONTINUE);

# 09.11.2016 - Kapandığı ayda tutar sıfırlansın istendi.

[ 'Tutar' ] =  N: 
	IF(SUBST(DB('Kredi Faiz', !Records, !Version, 'Opening '| ATTRS( 'Period', !Period, 'Year' ), 'Kapanış (yyyy-mm-dd)'), 1, 7) @= !Period, 0, CONTINUE);

# Giriş dönemlerine eşitle.

[ 'Tutar' ] =  N: 
	IF([ 'Ayrılan Karşılık' ] <> 0, DB('Kredi Faiz', !Records, !Version, 'Opening '| ATTRS( 'Period', !Period, 'Year' ), 'Tutar'), 0);

# Ödeme tarihi boş ise ödeme yapılmamalı.

[ 'Faiz Ödemesi' ] =  N: 
	IF(DB('Kredi Faiz', !Records, !Version, !Period, 'Ödeme Tarihi') @= '', 0, CONTINUE);

# Dönem sonu ödemeler.

[ 'Faiz Ödemesi' ] =  N: 
	IF(DB('Kredi Faiz', !Records, !Version, 'Opening '| ATTRS( 'Period', !Period, 'Year' ), 'Ödeme Periyodu') @= 'Dönem Sonu'
	& SUBST(!Period, 6, 2) @= '12', 120, CONTINUE);

# Yıllık ödemeler.

[ 'Faiz Ödemesi' ] =  N: 
	IF(DB('Kredi Faiz', !Records, !Version, 'Opening '| ATTRS( 'Period', !Period, 'Year' ), 'Ödeme Periyodu') @= 'Yıllık'
	& MONTH(DB('Kredi Faiz', !Records, !Version, 'Opening '| ATTRS( 'Period', !Period, 'Year' ), 'Açılış (yyyy-mm-dd)')) = NUMBR(SUBST(!Period, 6, 2)), 12, CONTINUE);

[ 'Faiz Ödemesi' ] =  N: 
	IF(DB('Kredi Faiz', !Records, !Version, !Period, 'Ödeme Tarihi') @<> ''
	& DB('Kredi Faiz', !Records, !Version, !Period, 'Bir Önceki Ödeme Tarihi') @= '', DB('Kredi Faiz', !Records, !Version, !Period, 'Güncel T. Faiz') \ DB('Kredi Faiz', !Records, !Version, 'Opening ' | ATTRS('Period', !Period, 'Year'), 'Gün') * (DAYNO(DB('Kredi Faiz', !Records, !Version, !Period, 'Ödeme Tarihi')) - DAYNO(DB('Kredi Faiz', !Records, !Version, 'Opening ' | ATTRS('Period', !Period, 'Year'), 'Açılış (yyyy-mm-dd)'))), CONTINUE);

[ 'Faiz Ödemesi' ] =  N: 
	IF(DB('Kredi Faiz', !Records, !Version, !Period, 'Ödeme Tarihi') @<> '', DB('Kredi Faiz', !Records, !Version, !Period, 'Güncel T. Faiz') \ DB('Kredi Faiz', !Records, !Version, 'Opening ' | ATTRS('Period', !Period, 'Year'), 'Gün') * (DAYNO(DB('Kredi Faiz', !Records, !Version, !Period, 'Ödeme Tarihi')) - DAYNO(DB('Kredi Faiz', !Records, !Version, !Period, 'Bir Önceki Ödeme Tarihi'))), CONTINUE);

# TL dönüşümleri.

[ 'Ayrılan Karşılık - TL' ] =  N: 
	IF(DB('Kredi Faiz', !Records, !Version, 'Opening '| ATTRS( 'Period', !Period, 'Year' ), 'Para Birimi') @= 'USD', [ 'Ayrılan Karşılık' ] * DB('V_P', !Version, !Period, 'Aysonu Kur - USD'), CONTINUE);

[ 'Ayrılan Karşılık - TL' ] =  N: 
	IF(DB('Kredi Faiz', !Records, !Version, 'Opening '| ATTRS( 'Period', !Period, 'Year' ), 'Para Birimi') @= 'EUR', [ 'Ayrılan Karşılık' ] * DB('V_P', !Version, !Period, 'Aysonu Kur - EUR'), CONTINUE);

[ 'Ayrılan Karşılık - TL' ] =  N: 
	IF(DB('Kredi Faiz', !Records, !Version, 'Opening '| ATTRS( 'Period', !Period, 'Year' ), 'Para Birimi') @= 'GBP', [ 'Ayrılan Karşılık' ] * DB('V_P', !Version, !Period, 'Aysonu Kur - GBP'), CONTINUE);

[ 'Ayrılan Karşılık - TL' ] =  N: 
	[ 'Ayrılan Karşılık' ];

[ 'Ayrılan Karşılık - Kredi - TL' ] =  N: 
	IF(DB('Kredi Faiz', !Records, !Version, 'Opening '| ATTRS( 'Period', !Period, 'Year' ), 'Kredi Türü') @= 'Kredi', [ 'Ayrılan Karşılık - TL' ], 0);

[ 'Ayrılan Karşılık - Tahvil - TL' ] =  N: 
	IF(DB('Kredi Faiz', !Records, !Version, 'Opening '| ATTRS( 'Period', !Period, 'Year' ), 'Kredi Türü') @= 'Tahvil', [ 'Ayrılan Karşılık - TL' ], 0);

[ 'Faiz Ödemesi - TL' ] =  N: 
	IF(DB('Kredi Faiz', !Records, !Version, 'Opening '| ATTRS( 'Period', !Period, 'Year' ), 'Para Birimi') @= 'USD', [ 'Faiz Ödemesi' ] * DB('V_P', !Version, !Period, 'Aysonu Kur - USD'), CONTINUE);

[ 'Faiz Ödemesi - TL' ] =  N: 
	IF(DB('Kredi Faiz', !Records, !Version, 'Opening '| ATTRS( 'Period', !Period, 'Year' ), 'Para Birimi') @= 'EUR', [ 'Faiz Ödemesi' ] * DB('V_P', !Version, !Period, 'Aysonu Kur - EUR'), CONTINUE);

[ 'Faiz Ödemesi - TL' ] =  N: 
	IF(DB('Kredi Faiz', !Records, !Version, 'Opening '| ATTRS( 'Period', !Period, 'Year' ), 'Para Birimi') @= 'GBP', [ 'Faiz Ödemesi' ] * DB('V_P', !Version, !Period, 'Aysonu Kur - GBP'), CONTINUE);

[ 'Faiz Ödemesi - TL' ] =  N: 
	[ 'Faiz Ödemesi' ];

[ 'Anapara Ödemesi - TL' ] =  N: 
	IF(DB('Kredi Faiz', !Records, !Version, 'Opening '| ATTRS( 'Period', !Period, 'Year' ), 'Para Birimi') @= 'USD', [ 'Anapara Ödemesi' ] * DB('V_P', !Version, !Period, 'Aysonu Kur - USD'), CONTINUE);

[ 'Anapara Ödemesi - TL' ] =  N: 
	IF(DB('Kredi Faiz', !Records, !Version, 'Opening '| ATTRS( 'Period', !Period, 'Year' ), 'Para Birimi') @= 'EUR', [ 'Anapara Ödemesi' ] * DB('V_P', !Version, !Period, 'Aysonu Kur - EUR'), CONTINUE);

[ 'Anapara Ödemesi - TL' ] =  N: 
	IF(DB('Kredi Faiz', !Records, !Version, 'Opening '| ATTRS( 'Period', !Period, 'Year' ), 'Para Birimi') @= 'GBP', [ 'Anapara Ödemesi' ] * DB('V_P', !Version, !Period, 'Aysonu Kur - GBP'), CONTINUE);

[ 'Anapara Ödemesi - TL' ] =  N: 
	[ 'Anapara Ödemesi' ];

[ 'Uzun Fin Karşılık' ] =  N: 
	[ 'Ayrılan Karşılık - TL' ];

[ 'Uzun Fin Ödeme' ] =  N: 
	[ 'Faiz Ödemesi - TL' ];

[ 'Kısa Fin Ödeme', 'Total Records' ] =  C: 
	IF(SUBST(!Period, 6, 2) @= '03'
	% SUBST(!Period, 6, 2) @= '06'
	% SUBST(!Period, 6, 2) @= '09'
	% SUBST(!Period, 6, 2) @= '12', [ 'Kısa Fin Karşılık' ] + DB('Kredi Faiz', !Records, !Version, STR(ATTRN('Period', !Period, 'Index')-1, 2, 0), 'Kısa Fin Karşılık') + DB('Kredi Faiz', !Records, !Version, STR(ATTRN('Period', !Period, 'Index')-2, 2, 0), 'Kısa Fin Karşılık'), 0);

[ 'Total Records', '37308' ] = C: IF(SCAN('Forecast', !Version) <> 0 & DB('V_P', !Version, !Period, 'Actual - Plan' ) @= 'Actual', DB( 'SPK Giriş Plan', '37308 / 300 Reclass', !Version, !Period, 'Amount' ), CONTINUE);

[ '37308', '2023-12' ] =  C: STET;

[ '37308' ] =  C: 
	IF(SUBST(!Period, 6, 2) @= '01', DB('Kredi Faiz', 'Total Records', !Version, ATTRS('Period', !Period, 'PrevMonth'), '37308') + [ 'Uzun Fin Karşılık' ] + [ 'Kısa Fin Karşılık' ] + [ 'Düzeltme Karşılık' ] - [ 'Uzun Fin Ödeme' ] - [ 'Kısa Fin Ödeme' ] - [ 'Düzeltme Ödeme' ], CONTINUE);

[ '37308' ] =  C: 
	DB('Kredi Faiz', !Records, !Version, ATTRS('Period', !Period, 'PrevMonth'), '37308') + [ 'Uzun Fin Karşılık' ] + [ 'Kısa Fin Karşılık' ] + [ 'Düzeltme Karşılık' ] - [ 'Uzun Fin Ödeme' ] - [ 'Kısa Fin Ödeme' ] - [ 'Düzeltme Ödeme' ];

[ 'Tutar - TL' ] =  N: 
	IF(DB('Kredi Faiz', !Records, !Version, 'Opening '| ATTRS( 'Period', !Period, 'Year' ), 'Para Birimi') @= 'USD', [ 'Tutar' ] * DB('V_P', !Version, !Period, 'Aysonu Kur - USD'), CONTINUE);

[ 'Tutar - TL' ] =  N: 
	IF(DB('Kredi Faiz', !Records, !Version, 'Opening '| ATTRS( 'Period', !Period, 'Year' ), 'Para Birimi') @= 'EUR', [ 'Tutar' ] * DB('V_P', !Version, !Period, 'Aysonu Kur - EUR'), CONTINUE);

[ 'Tutar - TL' ] =  N: 
	IF(DB('Kredi Faiz', !Records, !Version, 'Opening '| ATTRS( 'Period', !Period, 'Year' ), 'Para Birimi') @= 'GBP', [ 'Tutar' ] * DB('V_P', !Version, !Period, 'Aysonu Kur - GBP'), CONTINUE);

[ 'Tutar - TL' ] =  N: 
	[ 'Tutar' ];

[ 'Kredi Tutar - TL (Uzun Vadeli)' ] =  N: 
	IF(DB('Kredi Faiz', !Records, !Version, 'Opening '| ATTRS( 'Period', !Period, 'Year' ), 'Kredi Türü') @= 'Kredi'
	& DAYNO(DB('Kredi Faiz', !Records, !Version, 'Opening '| ATTRS( 'Period', !Period, 'Year' ), 'Kapanış (yyyy-mm-dd)')) - DAYNO(ATTRS('Period', !Period, 'LastDay')) > 365, [ 'Tutar - TL' ], 0);

[ 'Kredi Tutar - TL (Uzundan Kısaya Düşen)' ] =  N: 
	IF(DB('Kredi Faiz', !Records, !Version, 'Opening '| ATTRS( 'Period', !Period, 'Year' ), 'Kredi Türü') @= 'Kredi'
	& DAYNO(DB('Kredi Faiz', !Records, !Version, 'Opening '| ATTRS( 'Period', !Period, 'Year' ), 'Kapanış (yyyy-mm-dd)')) - DAYNO(ATTRS('Period', !Period, 'LastDay')) <= 365
	& DAYNO(DB('Kredi Faiz', !Records, !Version, 'Opening '| ATTRS( 'Period', !Period, 'Year' ), 'Kapanış (yyyy-mm-dd)')) > DAYNO(ATTRS('Period', !Period, 'FirstDay')), [ 'Tutar - TL' ], 0);

[ 'Tahvil Tutar - TL (Uzun Vadeli)' ] =  N: 
	IF(DB('Kredi Faiz', !Records, !Version, 'Opening '| ATTRS( 'Period', !Period, 'Year' ), 'Kredi Türü') @= 'Tahvil'
	& DAYNO(DB('Kredi Faiz', !Records, !Version, 'Opening '| ATTRS( 'Period', !Period, 'Year' ), 'Kapanış (yyyy-mm-dd)')) - DAYNO(ATTRS('Period', !Period, 'LastDay')) > 365, [ 'Tutar - TL' ], 0);

[ 'Tahvil Tutar - TL (Uzundan Kısaya Düşen)' ] =  N: 
	IF(DB('Kredi Faiz', !Records, !Version, 'Opening '| ATTRS( 'Period', !Period, 'Year' ), 'Kredi Türü') @= 'Tahvil'
	& DAYNO(DB('Kredi Faiz', !Records, !Version, 'Opening '| ATTRS( 'Period', !Period, 'Year' ), 'Kapanış (yyyy-mm-dd)')) - DAYNO(ATTRS('Period', !Period, 'LastDay')) <= 365
	& DAYNO(DB('Kredi Faiz', !Records, !Version, 'Opening '| ATTRS( 'Period', !Period, 'Year' ), 'Kapanış (yyyy-mm-dd)')) > DAYNO(ATTRS('Period', !Period, 'FirstDay')), [ 'Tutar - TL' ], 0);

FEEDERS;

[ 'Aktif', 'Tutar' ] => [ 'Tutar (Calc)' ];

[ 'Aktif', 'Kapanış (yyyy-mm-dd)' ] => [ 'Gün' ];

[ 'Aktif', 'Güncel Faiz Oranı' ] => [ 'Güncel T. Faiz' ];

[ 'Aktif', 'Anapara Ödemesi' ] => [ 'Anapara Ödemesi - TL' ];

[ 'Aktif', 'Ayrılan Karşılık' ] => DB('Kredi Faiz', !Records, !Version, ATTRS('Period', !Period, 'NextMonth'), !Kredi_Faiz_Meas), [ 'Faiz Ödemesi' ], [ 'Ödeme Tarihi' ], [ 'Bir Önceki Ödeme Tarihi' ], [ 'Ayrılan Karşılık - TL' ], [ 'Uzun Fin Karşılık' ], [ 'Faiz Ödemesi - TL' ], [ 'Uzun Fin Ödeme' ], [ 'Kısa Fin Karşılık' ], [ 'Kısa Fin Ödeme' ], [ '37308' ], [ 'Tutar' ], [ 'Tutar - TL' ], [ 'Kredi Tutar - TL (Uzun Vadeli)' ], [ 'Kredi Tutar - TL (Uzundan Kısaya Düşen)' ], [ 'Tahvil Tutar - TL (Uzun Vadeli)' ], [ 'Tahvil Tutar - TL (Uzundan Kısaya Düşen)' ], [ 'Ayrılan Karşılık - Kredi - TL' ], [ 'Ayrılan Karşılık - Tahvil - TL' ];

[ 'Aktif', 'Opening 2025', 'Güncel Faiz Oranı' ] => [ '2025-01', 'Ayrılan Karşılık' ];
[ 'Aktif', 'Opening 2025', 'Güncel Faiz Oranı' ] => [ '2025-02', 'Ayrılan Karşılık' ];
[ 'Aktif', 'Opening 2025', 'Güncel Faiz Oranı' ] => [ '2025-03', 'Ayrılan Karşılık' ];
[ 'Aktif', 'Opening 2025', 'Güncel Faiz Oranı' ] => [ '2025-04', 'Ayrılan Karşılık' ];
[ 'Aktif', 'Opening 2025', 'Güncel Faiz Oranı' ] => [ '2025-05', 'Ayrılan Karşılık' ];
[ 'Aktif', 'Opening 2025', 'Güncel Faiz Oranı' ] => [ '2025-06', 'Ayrılan Karşılık' ];
[ 'Aktif', 'Opening 2025', 'Güncel Faiz Oranı' ] => [ '2025-07', 'Ayrılan Karşılık' ];
[ 'Aktif', 'Opening 2025', 'Güncel Faiz Oranı' ] => [ '2025-08', 'Ayrılan Karşılık' ];
[ 'Aktif', 'Opening 2025', 'Güncel Faiz Oranı' ] => [ '2025-09', 'Ayrılan Karşılık' ];
[ 'Aktif', 'Opening 2025', 'Güncel Faiz Oranı' ] => [ '2025-10', 'Ayrılan Karşılık' ];
[ 'Aktif', 'Opening 2025', 'Güncel Faiz Oranı' ] => [ '2025-11', 'Ayrılan Karşılık' ];
[ 'Aktif', 'Opening 2025', 'Güncel Faiz Oranı' ] => [ '2025-12', 'Ayrılan Karşılık' ];

[ 'Aktif', 'Opening 2026', 'Güncel Faiz Oranı' ] => [ '2026-01', 'Ayrılan Karşılık' ];
[ 'Aktif', 'Opening 2026', 'Güncel Faiz Oranı' ] => [ '2026-02', 'Ayrılan Karşılık' ];
[ 'Aktif', 'Opening 2026', 'Güncel Faiz Oranı' ] => [ '2026-03', 'Ayrılan Karşılık' ];
[ 'Aktif', 'Opening 2026', 'Güncel Faiz Oranı' ] => [ '2026-04', 'Ayrılan Karşılık' ];
[ 'Aktif', 'Opening 2026', 'Güncel Faiz Oranı' ] => [ '2026-05', 'Ayrılan Karşılık' ];
[ 'Aktif', 'Opening 2026', 'Güncel Faiz Oranı' ] => [ '2026-06', 'Ayrılan Karşılık' ];
[ 'Aktif', 'Opening 2026', 'Güncel Faiz Oranı' ] => [ '2026-07', 'Ayrılan Karşılık' ];
[ 'Aktif', 'Opening 2026', 'Güncel Faiz Oranı' ] => [ '2026-08', 'Ayrılan Karşılık' ];
[ 'Aktif', 'Opening 2026', 'Güncel Faiz Oranı' ] => [ '2026-09', 'Ayrılan Karşılık' ];
[ 'Aktif', 'Opening 2026', 'Güncel Faiz Oranı' ] => [ '2026-10', 'Ayrılan Karşılık' ];
[ 'Aktif', 'Opening 2026', 'Güncel Faiz Oranı' ] => [ '2026-11', 'Ayrılan Karşılık' ];
[ 'Aktif', 'Opening 2026', 'Güncel Faiz Oranı' ] => [ '2026-12', 'Ayrılan Karşılık' ];

['Aktif', 'Total Records', 'Ayrılan Karşılık - Kredi - TL'] => DB( 'Genel Giderler', '8161101001', '9349999', 'Mali İşler - Finans', !Version, !Period, 'Hesaplanan Tutar' );
['Aktif', 'Total Records', 'Kısa Fin Karşılık'] => DB( 'Genel Giderler', '8161101001', '9349999', 'Mali İşler - Finans', !Version, !Period, 'Hesaplanan Tutar' );
['Aktif', 'Total Records', 'Düzeltme Karşılık'] => DB( 'Genel Giderler', '8161101001', '9349999', 'Mali İşler - Finans', !Version, !Period, 'Hesaplanan Tutar' );
['Aktif', 'Total Records', 'Ayrılan Karşılık - Tahvil - TL'] => DB( 'Genel Giderler', '9349999', '9349999', 'Mali İşler - Finans', !Version, !Period, 'Hesaplanan Tutar' );

['Aktif', 'Total Records', 'Kredi_Faiz_Meas':'37308'] => DB( 'Mali Tablo', '373', 'Kredi Faiz', !Version, !Period, 'Movement' );
['Aktif', 'Total Records', 'Kredi_Faiz_Meas':'Kredi Tutar - TL (Uzundan Kısaya Düşen)'] => DB( 'Mali Tablo', '300', 'Kredi Faiz', !Version, !Period, 'Movement' );
['Aktif', 'Total Records', 'Kredi_Faiz_Meas':'Tahvil Tutar - TL (Uzundan Kısaya Düşen)'] => DB( 'Mali Tablo', '304', 'Kredi Faiz', !Version, !Period, 'Movement' );
['Aktif', 'Total Records', 'Kredi_Faiz_Meas':'Kredi Tutar - TL (Uzun Vadeli)'] => DB( 'Mali Tablo', '400', 'Kredi Faiz', !Version, !Period, 'Movement' );
['Aktif', 'Total Records', 'Kredi_Faiz_Meas':'Tahvil Tutar - TL (Uzun Vadeli)'] => DB( 'Mali Tablo', '405', 'Kredi Faiz', !Version, !Period, 'Movement' );

['Aktif', 'Total Records', 'Kredi_Faiz_Meas':'37308'] => DB( 'SPK Giriş Plan', '37308 / 300 Reclass', !Version, !Period, 'Amount' );
