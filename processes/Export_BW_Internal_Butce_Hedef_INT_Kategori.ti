#region Prolog

#******* Source Cube ***********************************

vProcessName = GetProcessName;

v_CubeSourceName = 'Final Internal Prim';
v_ViewSourceName = 'Source'|'_'|v_CubeSourceName|'_'|vProcessName|'_'|pPeriod;

v_DimSourceName1 = 'Mal_Grupları';
v_DimSourceName2 = 'Masraf_Kar_Merkezleri';
v_DimSourceName3 = 'Day';
v_DimSourceName4 = 'Final_Internal_Prim_Meas';

v_SubSourceName1 = v_DimSourceName1|'_'|v_ViewSourceName|'_'|pPeriod;
v_SubSourceName2 = v_DimSourceName2|'_'|v_ViewSourceName|'_'|pPeriod;
v_SubSourceName3 = v_DimSourceName3|'_'|v_ViewSourceName|'_'|pPeriod;
v_SubSourceName4 = v_DimSourceName4|'_'|v_ViewSourceName|'_'|pPeriod;

IF(VIEWEXISTS(v_CubeSourceName, v_ViewSourceName)=1);
    VIEWDESTROY(v_CubeSourceName, v_ViewSourceName);
ENDIF;

IF(SUBSETEXISTS(v_DimSourceName1, v_SubSourceName1)=1);
    SUBSETDESTROY(v_DimSourceName1, v_SubSourceName1); 
ENDIF;
IF(SUBSETEXISTS(v_DimSourceName2, v_SubSourceName2)=1);
    SUBSETDESTROY(v_DimSourceName2, v_SubSourceName2); 
ENDIF;
IF(SUBSETEXISTS(v_DimSourceName3, v_SubSourceName3)=1);
    SUBSETDESTROY(v_DimSourceName3, v_SubSourceName3); 
ENDIF;
IF(SUBSETEXISTS(v_DimSourceName4, v_SubSourceName4)=1);
    SUBSETDESTROY(v_DimSourceName4, v_SubSourceName4); 
ENDIF;

VIEWCREATE(v_CubeSourceName, v_ViewSourceName);

ViewExtractSkipCalcsSet(v_CubeSourceName, v_ViewSourceName, 1);
ViewExtractSkipRuleValuesSet(v_CubeSourceName, v_ViewSourceName, 1);
ViewExtractSkipZeroesSet(v_CubeSourceName, v_ViewSourceName, 1);

MDX1 = '{TM1FILTERBYLEVEL({TM1DRILLDOWNMEMBER({ ['|v_DimSourceName1|'].[Koçtaş Kategori Toplam]},ALL,RECURSIVE)}, 0)}';
SUBSETCREATEBYMDX(v_SubSourceName1, MDX1, 0);
SubsetMDXSet(v_DimSourceName1, v_SubSourceName1, '' );

MDX2 = '{TM1FILTERBYLEVEL({TM1SUBSETALL(['|v_DimSourceName2|'])},0)}';
SUBSETCREATEBYMDX(v_SubSourceName2, MDX2, 0);
SubsetMDXSet(v_DimSourceName2, v_SubSourceName2, '' );

MDX3 = '{TM1FILTERBYLEVEL({TM1DRILLDOWNMEMBER({ ['|v_DimSourceName3|'].['|pPeriod|']},ALL,RECURSIVE)}, 0)}';
SUBSETCREATEBYMDX (v_SubSourceName3, MDX3,0 );
SubsetMDXSet(v_DimSourceName3, v_SubSourceName3, '' );

SubsetCreate(v_DimSourceName4, v_SubSourceName4, 0);
SubsetElementInsert(v_DimSourceName4, v_SubSourceName4, 'Perakende Satış (INT-Kategori)', 1);
SubsetElementInsert(v_DimSourceName4, v_SubSourceName4, 'Perakende Margin (INT-Kategori)', 1);
SubsetElementInsert(v_DimSourceName4, v_SubSourceName4, 'Ticari Satış (INT-Kategori)', 1);
SubsetElementInsert(v_DimSourceName4, v_SubSourceName4, 'Ticari Margin (INT-Kategori)', 1);
SubsetElementInsert(v_DimSourceName4, v_SubSourceName4, 'Mobil Ticari (INT-Kategori)', 1);
SubsetElementInsert(v_DimSourceName4, v_SubSourceName4, 'Mobil Ticari Margin (INT-Kategori)', 1);
SubsetElementInsert(v_DimSourceName4, v_SubSourceName4, '3P Satış (INT-Kategori)', 1);
SubsetElementInsert(v_DimSourceName4, v_SubSourceName4, '3P Margin (INT-Kategori)', 1);
SubsetElementInsert(v_DimSourceName4, v_SubSourceName4, 'Kiosk Sales (INT-Kategori)', 1);
SubsetElementInsert(v_DimSourceName4, v_SubSourceName4, 'Handyman Sales (INT-Kategori)', 1);

VIEWSUBSETASSIGN(v_CubeSourceName, v_ViewSourceName, v_DimSourceName1, v_SubSourceName1);
VIEWSUBSETASSIGN(v_CubeSourceName, v_ViewSourceName, v_DimSourceName2, v_SubSourceName2);
VIEWSUBSETASSIGN(v_CubeSourceName, v_ViewSourceName, v_DimSourceName3, v_SubSourceName3);
VIEWSUBSETASSIGN(v_CubeSourceName, v_ViewSourceName, v_DimSourceName4, v_SubSourceName4);

#Data source assignment
DataSourceType = 'View';
DataSourceNameForServer = v_CubeSourceName;
DatasourceCubeview = v_ViewSourceName;

ODBCOpen('PA', 'pawuser', 'p(>.z28n)Uk7');
ODBCOutPut('PA', 'TRUNCATE TABLE tm1satis.dbo.INTERNAL_BUTCE_KATEGORI_HEDEF');
# ODBCOutPut('PA', 'DELETE FROM tm1satis.dbo.INTERNAL_BUTCE_KATEGORI_HEDEF WHERE MAGAZA IN (2350600, 2340700, 6368700);');
# ODBCOutPut('PA', 'DELETE FROM tm1satis.dbo.INTERNAL_BUTCE_KATEGORI_HEDEF WHERE BUTCETIPI IN (''MT'', ''MTM'');');

vSQL = '';
vRecordtoAdd = '';
vRecord = 0;
vExportForEveryxRecords = 950;
#endregion
#region Data

vGun = SUBST(Gun, 1, 4)|'-'|SUBST(Gun, 6, 2)|'-'|SUBST(Gun , 9, 2);

IF(Parametre@='Perakende Satış (INT-Kategori)' % Parametre@='Kiosk Sales (INT-Kategori)' % Parametre@='Handyman Sales (INT-Kategori)');
    sParam='PS' ;
ELSEIF(Parametre@='Perakende Margin (INT-Kategori)');  
    sParam='PCM';
ELSEIF(Parametre@='Ticari Satış (INT-Kategori)');
    sParam='TS' ;
ELSEIF(Parametre@='Ticari Margin (INT-Kategori)');  
    sParam='TCM';
ELSEIF(Parametre@='Mobil Ticari (INT-Kategori)');
    sParam='MT' ;
ELSEIF(Parametre@='Mobil Ticari Margin (INT-Kategori)');
    sParam='MTM' ;
ELSEIF(Parametre@='3P Satış (INT-Kategori)');
    sParam='K3P' ;
ELSEIF(Parametre@='3P Margin (INT-Kategori)');
    sParam='K3PM' ;
ENDIF;

IF( vSQL @= '');
    vSQL = 'INSERT INTO tm1satis.dbo.INTERNAL_BUTCE_KATEGORI_HEDEF(MAGAZA ,MAL_GRUBU, TARIH, BUTCETIPI, value, USTA_BUTCE, KIOSK_BUTCE) VALUES';
ENDIF;

if (Parametre @= 'Handyman Sales (INT-Kategori)'); 
    vRecordToAdd = '('''|ATTRS( 'Masraf_Kar_Merkezleri', Magaza, 'Kar Merkezi Code' )|''', '''|Reyon|''', '''|vGun|''', '''|sParam|''', '|NumbertoStringEx(0,'0.000000','.','')|', '|NumbertoStringEx(Value,'0.000000','.','')|', '|NumbertoStringEx(0,'0.000000','.','')|'  )'; 
elseif (Parametre @= 'Kiosk Sales (INT-Kategori)');
    vRecordToAdd = '('''|ATTRS( 'Masraf_Kar_Merkezleri', Magaza, 'Kar Merkezi Code' )|''', '''|Reyon|''', '''|vGun|''', '''|sParam|''', '|NumbertoStringEx(0,'0.000000','.','')|', '|NumbertoStringEx(0,'0.000000','.','')|', '|NumbertoStringEx(Value,'0.000000','.','')|'  )'; 
else;
    vRecordToAdd = '('''|ATTRS( 'Masraf_Kar_Merkezleri', Magaza, 'Kar Merkezi Code' )|''', '''|Reyon|''', '''|vGun|''', '''|sParam|''', '|NumbertoStringEx(Value,'0.000000','.','')|', '|NumbertoStringEx(0,'0.000000','.','')|', '|NumbertoStringEx(0,'0.000000','.','')|'  )'; 
endif;

vSQL = vSQL | IF( vRecord > 0 , ',', '') | vRecordToAdd ;

IF( vRecord = vExportForEveryxRecords );
    ODBCOutPut('PA', vSQL);
    vSQL = '';
    vRecord = 0;
ELSE;
    vRecord = vRecord + 1;
ENDIF;
#endregion
#region Epilog

IF( vSQL @<> '');
    ODBCOutput('PA', vSQL);
ENDIF;

ODBCClose('PA');

###---Source View Destroy----###

IF(VIEWEXISTS(v_CubeSourceName, v_ViewSourceName)=1);
    VIEWDESTROY(v_CubeSourceName,v_ViewSourceName);
ENDIF;

IF(SUBSETEXISTS(v_DimSourceName1,v_SubSourceName1)=1);
    SUBSETDESTROY(v_DimSourceName1,v_SubSourceName1); 
ENDIF;
IF(SUBSETEXISTS(v_DimSourceName2,v_SubSourceName2)=1);
    SUBSETDESTROY(v_DimSourceName2,v_SubSourceName2); 
ENDIF;
IF(SUBSETEXISTS(v_DimSourceName3,v_SubSourceName3)=1);
    SUBSETDESTROY(v_DimSourceName3,v_SubSourceName3); 
ENDIF;
IF(SUBSETEXISTS(v_DimSourceName4,v_SubSourceName4)=1);
    SUBSETDESTROY(v_DimSourceName4,v_SubSourceName4); 
ENDIF;
ExecuteProcess( 'Execute_Send_Email', 'pPeriod', pPeriod, 'pProcessName', vProcessName );
#endregion