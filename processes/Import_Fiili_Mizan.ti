#region Prolog

CubeName = 'Fiili Mizan';
CubeSetLogChanges (CubeName, 0);

#******* Target Cube ***********************************

vProcessName = GetProcessName;

v_CubeName = 'Fiili Mizan';
v_ViewName = 'VZO'|'_'|v_CubeName|'_'|vProcessName;

v_Dim1Name = 'Mizan_Hesapları';
v_Dim2Name = 'Version_Actual';
v_Dim3Name = 'Period';
v_Dim4Name = 'Fiili_Mizan_Meas';

v_Subs1Name = v_Dim1Name|'_'|v_ViewName;
v_Subs2Name = v_Dim2Name|'_'|v_ViewName;
v_Subs3Name = v_Dim3Name|'_'|v_ViewName;
v_Subs4Name = v_Dim4Name|'_'|v_ViewName;

IF(VIEWEXISTS(v_CubeName, v_ViewName)=1);
    VIEWDESTROY(v_CubeName, v_ViewName);
ENDIF;

IF(SUBSETEXISTS(v_Dim1Name, v_Subs1Name)=1);
    SUBSETDESTROY(v_Dim1Name, v_Subs1Name);
ENDIF;
IF(SUBSETEXISTS(v_Dim2Name, v_Subs2Name)=1);
    SUBSETDESTROY(v_Dim2Name, v_Subs2Name);
ENDIF;
IF(SUBSETEXISTS(v_Dim3Name, v_Subs3Name)=1);
    SUBSETDESTROY(v_Dim3Name, v_Subs3Name);
ENDIF;
IF(SUBSETEXISTS(v_Dim4Name, v_Subs4Name)=1);
    SUBSETDESTROY(v_Dim4Name, v_Subs4Name);
ENDIF;

VIEWCREATE(v_CubeName, v_ViewName);

MDX1 = '{TM1FILTERBYLEVEL({TM1SUBSETALL(['|v_Dim1Name|'])},0)}';
SUBSETCREATEBYMDX(v_Subs1Name, MDX1, 0);

SubsetCreate(v_Dim2Name, v_Subs2Name, 0);
SubsetElementInsert(v_Dim2Name, v_Subs2Name, 'Fiili', 1);

SubsetCreate(v_Dim3Name, v_Subs3Name, 0);
SubsetElementInsert(v_Dim3Name, v_Subs3Name, pDonem, 1);

SubsetCreate(v_Dim4Name, v_Subs4Name, 0);
SubsetElementInsert(v_Dim4Name, v_Subs4Name, 'Borç Bakiyesi', 1);
SubsetElementInsert(v_Dim4Name, v_Subs4Name, 'Alacak Bakiyesi', 1);
SubsetElementInsert(v_Dim4Name, v_Subs4Name, 'Borç - YTD', 1);
SubsetElementInsert(v_Dim4Name, v_Subs4Name, 'Alacak - YTD', 1);

VIEWSUBSETASSIGN(v_CubeName, v_ViewName, v_Dim1Name, v_Subs1Name);
VIEWSUBSETASSIGN(v_CubeName, v_ViewName, v_Dim2Name, v_Subs2Name);
VIEWSUBSETASSIGN(v_CubeName, v_ViewName, v_Dim3Name, v_Subs3Name);
VIEWSUBSETASSIGN(v_CubeName, v_ViewName, v_Dim4Name, v_Subs4Name);

ViewZeroOut(v_CubeName, v_ViewName);
#endregion
#region Metadata

# Hesap kodundan önekleri çıkar
vHes1 = SUBST(vHes, 1, 1);
vHes3 = SUBST(vHes, 1, 3);

IF( DIMIX('Mizan_Hesapları', vHes) = 0 );
    # Ana kategorilere ekle
    IF( vHes1 @= '6' );
        DimensionElementComponentAddDirect('Mizan_Hesapları', 'Income Accounts', vHes, 1);
    ENDIF;

    IF( vHes1 @= '8' );
        DimensionElementComponentAddDirect('Mizan_Hesapları', 'Expense Accounts', vHes, 1);
        DimensionElementComponentAddDirect('Mizan_Hesapları', 'Expense Accounts (Excl. Depreciation)', vHes, 1);
    ENDIF;

    # 3 haneli hesap grubu varsa, altına ekle
    IF( DIMIX('Mizan_Hesapları', vHes3) <> 0 );
        DimensionElementComponentAddDirect('Mizan_Hesapları', vHes3, vHes, 1);
        DimensionElementComponentAddDirect('Mizan_Hesapları', vHes1, vHes3, 1);
        DimensionElementComponentAddDirect('Mizan_Hesapları', 'Toplam', vHes1, 1);
    ELSE;
        # 3 haneli hesap grubu yoksa oluştur, sonra ekle
        DimensionElementInsertDirect('Mizan_Hesapları', '', vHes3, 'N');
        DimensionElementComponentAddDirect('Mizan_Hesapları', vHes3, vHes, 1);
        DimensionElementComponentAddDirect('Mizan_Hesapları', vHes1, vHes3, 1);
        DimensionElementComponentAddDirect('Mizan_Hesapları', 'Toplam', vHes1, 1);
    ENDIF;
ENDIF;

IF( DIMIX('Mizan_Hesapları_Uclu', vHes3) = 0 );
    IF( vHes1 @= '1' % vHes1 @= '2' );
        DimensionElementComponentAddDirect('Mizan_Hesapları_Uclu', 'Varlıklar', vHes3, 1);
    ELSEIF( vHes1 @= '3' % vHes1 @= '4' );
        DimensionElementComponentAddDirect('Mizan_Hesapları_Uclu', 'Yükümlülükler', vHes3, 1);
    ELSEIF( vHes1 @= '5' );
        DimensionElementComponentAddDirect('Mizan_Hesapları_Uclu', 'Sermaye', vHes3, 1);
    ELSEIF( vHes1 @= '6' );
        DimensionElementComponentAddDirect('Mizan_Hesapları_Uclu', 'Gelir/Gider', vHes3, 1);
    ENDIF;
ENDIF;

#endregion
#region Data

IF( SUBST( pDonem, 6, 2 ) @= '01' );
    Data = Oca+Devir;
    Data_YTD = Oca+Devir;
ELSEIF( SUBST( pDonem, 6, 2 ) @= '02' );
    Data = Sub;
    Data_YTD = Oca+Sub+Devir;
ELSEIF( SUBST( pDonem, 6, 2 ) @= '03' );
    Data = Mar;
    Data_YTD = Oca+Sub+Mar+Devir;
ELSEIF( SUBST( pDonem, 6, 2 ) @= '04' );
    Data = Nis;
    Data_YTD = Oca+Sub+Mar+Nis;
ELSEIF( SUBST( pDonem, 6, 2 ) @= '05' );
    Data = May;
    Data_YTD = Oca+Sub+Mar+Nis+May;
ELSEIF( SUBST( pDonem, 6, 2 ) @= '06' );
    Data = Haz;
    Data_YTD = Oca+Sub+Mar+Nis+May+Haz;
ELSEIF( SUBST( pDonem, 6, 2 ) @= '07' );
    Data = Tem;
    Data_YTD = Oca+Sub+Mar+Nis+May+Haz+Tem;
ELSEIF( SUBST( pDonem, 6, 2 ) @= '08' );
    Data = Agu;
    Data_YTD = Oca+Sub+Mar+Nis+May+Haz+Tem+Agu;
ELSEIF( SUBST( pDonem, 6, 2 ) @= '09' );
    Data = Eyl;
    Data_YTD = Oca+Sub+Mar+Nis+May+Haz+Tem+Agu+Eyl;
ELSEIF( SUBST( pDonem, 6, 2 ) @= '10' );
    Data = Eki;
    Data_YTD = Oca+Sub+Mar+Nis+May+Haz+Tem+Agu+Eyl+Eki;
ELSEIF( SUBST( pDonem, 6, 2 ) @= '11' );
    Data = Kas;
    Data_YTD = Oca+Sub+Mar+Nis+May+Haz+Tem+Agu+Eyl+Eki+Kas;
ELSEIF( SUBST( pDonem, 6, 2 ) @= '12' );
    Data = Ara;
    Data_YTD = Oca+Sub+Mar+Nis+May+Haz+Tem+Agu+Eyl+Eki+Kas+Ara;
ENDIF;

IF( DIMIX( 'Mizan_Hesapları', vHes ) <> 0 );
    IF( vDetay @= 'S' );
        CellIncrementN( Data, 'Fiili Mizan', vHes, 'Fiili', pDonem, 'Borç Bakiyesi' );
        CellIncrementN( Data_YTD, 'Fiili Mizan', vHes, 'Fiili', pDonem, 'Borç - YTD' );
    ENDIF;

    IF( vDetay @= 'H' );
        CellIncrementN( Data, 'Fiili Mizan', vHes, 'Fiili', pDonem, 'Alacak Bakiyesi' );
        CellIncrementN( Data_YTD, 'Fiili Mizan', vHes, 'Fiili', pDonem, 'Alacak - YTD' );
    ENDIF;
ENDIF;
#endregion
#region Epilog

CubeName = 'Fiili Mizan';
CubeSetLogChanges(CubeName, 1);

###---Target View Destroy----###

IF(VIEWEXISTS(v_CubeName, v_ViewName)=1);
    VIEWDESTROY(v_CubeName,v_ViewName);
ENDIF;

IF(SUBSETEXISTS(v_Dim1Name,v_Subs1Name)=1);
    SUBSETDESTROY(v_Dim1Name,v_Subs1Name); 
ENDIF;
IF(SUBSETEXISTS(v_Dim2Name,v_Subs2Name)=1);
    SUBSETDESTROY(v_Dim2Name,v_Subs2Name); 
ENDIF;
IF(SUBSETEXISTS(v_Dim3Name,v_Subs3Name)=1);
    SUBSETDESTROY(v_Dim3Name,v_Subs3Name); 
ENDIF;
IF(SUBSETEXISTS(v_Dim4Name,v_Subs4Name)=1);
    SUBSETDESTROY(v_Dim4Name,v_Subs4Name); 
ENDIF;
#endregion