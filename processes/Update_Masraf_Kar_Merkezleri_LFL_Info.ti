#region Prolog

CubeSetLogChanges ('MM_V_P', 0);

#******* Source Cube ***********************************

vProcessName = GetProcessName;

v_CubeSourceName = 'MM_V';
v_ViewSourceName = 'Source'|'_'|v_CubeSourceName|'_'|vProcessName;

v_DimSourceName1 = 'Masraf_Kar_Merkezleri';
v_DimSourceName2 = 'Version';
v_DimSourceName3 = 'MM_V_Meas';

v_SubSourceName1 = v_DimSourceName1|'_'|v_ViewSourceName;
v_SubSourceName2 = v_DimSourceName2|'_'|v_ViewSourceName;
v_SubSourceName3 = v_DimSourceName3|'_'|v_ViewSourceName;

IF(VIEWEXISTS(v_CubeSourceName, v_ViewSourceName)=1);
    VIEWDESTROY(v_CubeSourceName, v_ViewSourceName);
ENDIF;

IF(SUBSETEXISTS(v_DimSourceName1, v_SubSourceName1)=1);
    SUBSETDESTROY(v_DimSourceName1, v_SubSourceName1); 
ENDIF;
IF(SUBSETEXISTS(v_DimSourceName2, v_SubSourceName2)=1);
    SUBSETDESTROY(v_DimSourceName2, v_SubSourceName2); 
ENDIF;
IF(SUBSETEXISTS(v_DimSourceName3, v_SubSourceName3)=1);
    SUBSETDESTROY(v_DimSourceName3, v_SubSourceName3); 
ENDIF;

VIEWCREATE(v_CubeSourceName, v_ViewSourceName);

ViewExtractSkipCalcsSet(v_CubeSourceName, v_ViewSourceName, 1);
ViewExtractSkipRuleValuesSet(v_CubeSourceName, v_ViewSourceName, 1);
ViewExtractSkipZeroesSet(v_CubeSourceName, v_ViewSourceName, 0);

MDX1 = '{TM1FILTERBYLEVEL({TM1SUBSETALL(['|v_DimSourceName1|'])},0)}';
SUBSETCREATEBYMDX(v_SubSourceName1, MDX1, 0);

SubsetCreate(v_DimSourceName2, v_SubSourceName2, 0);
SubsetElementInsert(v_DimSourceName2, v_SubSourceName2, pVersion, 1);

SubsetCreate(v_DimSourceName3, v_SubSourceName3, 0);
SubsetElementInsert(v_DimSourceName3, v_SubSourceName3, 'Açılış Tarihi (yyyy-mm-dd)', 1);
SubsetElementInsert(v_DimSourceName3, v_SubSourceName3, 'Kapanış Tarihi (yyyy-mm-dd)', 1);

VIEWSUBSETASSIGN(v_CubeSourceName, v_ViewSourceName, v_DimSourceName1, v_SubSourceName1);
VIEWSUBSETASSIGN(v_CubeSourceName, v_ViewSourceName, v_DimSourceName2, v_SubSourceName2);
VIEWSUBSETASSIGN(v_CubeSourceName, v_ViewSourceName, v_DimSourceName3, v_SubSourceName3);

#Data source assignment
DataSourceType = 'View';
DataSourceNameForServer = v_CubeSourceName;
DatasourceCubeview = v_ViewSourceName;

#******* Target Cube ***********************************

vProcessName = GetProcessName;

v_CubeName = 'MM_V_P';
v_ViewName = 'VZO'|'_'|v_CubeName|'_'|vProcessName;

v_Dim1Name = 'Masraf_Kar_Merkezleri';
v_Dim2Name = 'Version';
v_Dim3Name = 'Period';
v_Dim4Name = 'MM_V_P_Meas';

v_Subs1Name = v_Dim1Name|'_'|v_ViewName;
v_Subs2Name = v_Dim2Name|'_'|v_ViewName;
v_Subs3Name = v_Dim3Name|'_'|v_ViewName;
v_Subs4Name = v_Dim4Name|'_'|v_ViewName;

IF(VIEWEXISTS(v_CubeName, v_ViewName)=1);
    VIEWDESTROY(v_CubeName, v_ViewName);
ENDIF;

IF(SUBSETEXISTS(v_Dim1Name, v_Subs1Name)=1);
    SUBSETDESTROY(v_Dim1Name, v_Subs1Name);
ENDIF;
IF(SUBSETEXISTS(v_Dim2Name, v_Subs2Name)=1);
    SUBSETDESTROY(v_Dim2Name, v_Subs2Name);
ENDIF;
IF(SUBSETEXISTS(v_Dim3Name, v_Subs3Name)=1);
    SUBSETDESTROY(v_Dim3Name, v_Subs3Name);
ENDIF;
IF(SUBSETEXISTS(v_Dim4Name, v_Subs4Name)=1);
    SUBSETDESTROY(v_Dim4Name, v_Subs4Name);
ENDIF;

VIEWCREATE(v_CubeName, v_ViewName);

MDX1 = '{TM1FILTERBYLEVEL({TM1SUBSETALL(['|v_Dim1Name|'])},0)}';
SUBSETCREATEBYMDX(v_Subs1Name, MDX1, 0);

SubsetCreate(v_Dim2Name, v_Subs2Name, 0);
SubsetElementInsert(v_Dim2Name, v_Subs2Name, pVersion, 1);

sYear = SUBST(pVersion, LONG(pVersion)-4+1, 4);
MDX3 = '{TM1FILTERBYLEVEL({TM1SUBSETALL(['|v_Dim3Name|'])},0)}';
SUBSETCREATEBYMDX(v_Subs3Name, MDX3, 0);

SubsetCreate(v_Dim4Name, v_Subs4Name, 0);
SubsetElementInsert(v_Dim4Name, v_Subs4Name, 'LFL / Non-LFL', 1);

VIEWSUBSETASSIGN(v_CubeName, v_ViewName, v_Dim1Name, v_Subs1Name);
VIEWSUBSETASSIGN(v_CubeName, v_ViewName, v_Dim2Name, v_Subs2Name);
VIEWSUBSETASSIGN(v_CubeName, v_ViewName, v_Dim3Name, v_Subs3Name);
VIEWSUBSETASSIGN(v_CubeName, v_ViewName, v_Dim4Name, v_Subs4Name);

ViewZeroOut(v_CubeName, v_ViewName);
#endregion
#region Data

#AÇILIŞ
if (MM_V_Meas @= 'Açılış Tarihi (yyyy-mm-dd)' & Value @<> ''); 
    sAcilisYear = SUBST( Value, 1, 4 );
    sAcilisMonth = SUBST( Value, 6, 2 );
    sAcilisDay = SUBST( Value, 9, 2 );

    vSerialNo = (2022 - NUMBR( sAcilisYear )) * -12 + NUMBR( sAcilisMonth );
    vSerialStartNo = vSerialNo;

    sWriteStartMonth = sAcilisYear|'-'|sAcilisMonth;

    if (DIMIX( 'Period',sWriteStartMonth ) <> 0); 
        sWriteMonth = sWriteStartMonth;
    else;    
        sWriteMonth = '2022-01';
    endif;

    while (vSerialNo <= 108); 
        if ( vSerialNo > 0 ); 
            if ((vSerialNo - vSerialStartNo) < 14 & NUMBR(sAcilisDay) >= 22); 
                if (SUBST( sWriteMonth, 1, 4 ) @= sYear); 
                    CellPutS( 'Non-LFL', 'MM_V_P', Masraf_Kar_Merkezleri, pVersion, sWriteMonth, 'LFL / Non-LFL' );
                endif;
                sWriteMonth = ATTRS( 'Period', sWriteMonth, 'NextMonth' );
            elseif((vSerialNo - vSerialStartNo) < 13 & NUMBR(sAcilisDay) < 22);    
                if (SUBST( sWriteMonth, 1, 4 ) @= sYear); 
                    CellPutS( 'Non-LFL', 'MM_V_P', Masraf_Kar_Merkezleri, pVersion, sWriteMonth, 'LFL / Non-LFL' );
                endif;
                sWriteMonth = ATTRS( 'Period', sWriteMonth, 'NextMonth' );
            else;
                if (SUBST( sWriteMonth, 1, 4 ) @= sYear); 
                    CellPutS( 'LFL', 'MM_V_P', Masraf_Kar_Merkezleri, pVersion, sWriteMonth, 'LFL / Non-LFL' );          
                endif;
                sWriteMonth = ATTRS( 'Period', sWriteMonth, 'NextMonth' );  
            endif;
        endif; 
        vSerialNo = vSerialNo + 1;
    end; 
endif;

#KAPANIŞ
if (MM_V_Meas @= 'Kapanış Tarihi (yyyy-mm-dd)' & Value @<> ''); 
    sKapanisYear = SUBST( Value, 1, 4 );
    sKapanisMonth = SUBST( Value, 6, 2 );
    sKapanisDay = SUBST( Value, 9, 2 );

    vSerialNo = (2022 - NUMBR( sKapanisYear )) * -12 + NUMBR( sKapanisMonth );
    vSerialStartNo = vSerialNo;

    sWriteStartMonth = sKapanisYear|'-'|sKapanisMonth;

    if (DIMIX( 'Period',sWriteStartMonth ) <> 0); 
        sWriteMonth = sWriteStartMonth;
    else;    
        sWriteMonth = '2022-01';
    endif;

    while (vSerialNo <= 108); 
        if ( vSerialNo > 0 );
            if ((vSerialNo - vSerialStartNo) < 2 & DIMIX('Period', ATTRS( 'Period', sWriteMonth, '2PrevMonth')) <> 0 ); 
                if (SUBST( ATTRS( 'Period', sWriteMonth, '2PrevMonth' ), 1, 4 ) @= sYear);
                    CellPutS( 'Non-LFL', 'MM_V_P', Masraf_Kar_Merkezleri, pVersion, ATTRS( 'Period', sWriteMonth, '2PrevMonth' ), 'LFL / Non-LFL' );
                endif;
                if (SUBST( sWriteMonth, 1, 4 ) @= sYear);
                    CellPutS( 'KAPALI', 'MM_V_P', Masraf_Kar_Merkezleri, pVersion, sWriteMonth, 'LFL / Non-LFL' );
                endif;
                sWriteMonth = ATTRS( 'Period', sWriteMonth, 'NextMonth' );               
            else;
                if (SUBST( sWriteMonth, 1, 4 ) @= sYear); 
                    CellPutS( 'KAPALI', 'MM_V_P', Masraf_Kar_Merkezleri, pVersion, sWriteMonth, 'LFL / Non-LFL' );
                endif;
                sWriteMonth = ATTRS( 'Period', sWriteMonth, 'NextMonth' );  
            endif;
        endif;
        vSerialNo = vSerialNo + 1;
    end; 
endif;
#endregion
#region Epilog

CubeSetLogChanges ('MM_V_P', 1);

###---Source View Destroy----###

IF(VIEWEXISTS(v_CubeSourceName, v_ViewSourceName)=1);
    VIEWDESTROY(v_CubeSourceName,v_ViewSourceName);
ENDIF;

IF(SUBSETEXISTS(v_DimSourceName1,v_SubSourceName1)=1);
    SUBSETDESTROY(v_DimSourceName1,v_SubSourceName1); 
ENDIF;
IF(SUBSETEXISTS(v_DimSourceName2,v_SubSourceName2)=1);
    SUBSETDESTROY(v_DimSourceName2,v_SubSourceName2); 
ENDIF;
IF(SUBSETEXISTS(v_DimSourceName3,v_SubSourceName3)=1);
    SUBSETDESTROY(v_DimSourceName3,v_SubSourceName3); 
ENDIF;

###---Target View Destroy----###

IF(VIEWEXISTS(v_CubeName, v_ViewName)=1);
    VIEWDESTROY(v_CubeName,v_ViewName);
ENDIF;

IF(SUBSETEXISTS(v_Dim1Name,v_Subs1Name)=1);
    SUBSETDESTROY(v_Dim1Name,v_Subs1Name); 
ENDIF;
IF(SUBSETEXISTS(v_Dim2Name,v_Subs2Name)=1);
    SUBSETDESTROY(v_Dim2Name,v_Subs2Name); 
ENDIF;
IF(SUBSETEXISTS(v_Dim3Name,v_Subs3Name)=1);
    SUBSETDESTROY(v_Dim3Name,v_Subs3Name); 
ENDIF;
IF(SUBSETEXISTS(v_Dim4Name,v_Subs4Name)=1);
    SUBSETDESTROY(v_Dim4Name,v_Subs4Name); 
ENDIF;

ExecuteProcess( 'Update_Masraf_Kar_Merkezleri_LFL_Info_2', 'pVersion', pVersion );
#endregion