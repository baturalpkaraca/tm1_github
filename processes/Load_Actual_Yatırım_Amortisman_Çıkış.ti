#region Prolog

CubeName = 'Yatırım Amortisman';
CubeSetLogChanges (CubeName, 0);

#******* Source Cube ***********************************

vProcessName = GetProcessName;

v_CubeSourceName = 'Yatırım Giriş Fiili';
v_ViewSourceName = 'Source'|'_'|v_CubeSourceName|'_'|vProcessName;

v_DimSourceName1 = 'Masraf_Kar_Merkezleri';
v_DimSourceName2 = 'Duran_Varlık_Sınıfları';
v_DimSourceName3 = 'Yatırım_Giriş_Detay';
v_DimSourceName4 = 'Version_Actual';
v_DimSourceName5 = 'Period';
v_DimSourceName6 = 'Yatırım_Giriş_Fiili_Meas';

v_SubSourceName1 = v_DimSourceName1|'_'|v_ViewSourceName;
v_SubSourceName2 = v_DimSourceName2|'_'|v_ViewSourceName;
v_SubSourceName3 = v_DimSourceName3|'_'|v_ViewSourceName;
v_SubSourceName4 = v_DimSourceName4|'_'|v_ViewSourceName;
v_SubSourceName5 = v_DimSourceName5|'_'|v_ViewSourceName;
v_SubSourceName6 = v_DimSourceName6|'_'|v_ViewSourceName;

IF(VIEWEXISTS(v_CubeSourceName, v_ViewSourceName)=1);
    VIEWDESTROY(v_CubeSourceName, v_ViewSourceName);
ENDIF;

IF(SUBSETEXISTS(v_DimSourceName1, v_SubSourceName1)=1);
    SUBSETDESTROY(v_DimSourceName1, v_SubSourceName1); 
ENDIF;
IF(SUBSETEXISTS(v_DimSourceName2, v_SubSourceName2)=1);
    SUBSETDESTROY(v_DimSourceName2, v_SubSourceName2); 
ENDIF;
IF(SUBSETEXISTS(v_DimSourceName3, v_SubSourceName3)=1);
    SUBSETDESTROY(v_DimSourceName3, v_SubSourceName3); 
ENDIF;
IF(SUBSETEXISTS(v_DimSourceName4, v_SubSourceName4)=1);
    SUBSETDESTROY(v_DimSourceName4, v_SubSourceName4); 
ENDIF;
IF(SUBSETEXISTS(v_DimSourceName5, v_SubSourceName5)=1);
    SUBSETDESTROY(v_DimSourceName5, v_SubSourceName5); 
ENDIF;
IF(SUBSETEXISTS(v_DimSourceName6, v_SubSourceName6)=1);
    SUBSETDESTROY(v_DimSourceName6, v_SubSourceName6); 
ENDIF;

VIEWCREATE(v_CubeSourceName, v_ViewSourceName);

ViewExtractSkipCalcsSet(v_CubeSourceName, v_ViewSourceName, 1);
ViewExtractSkipRuleValuesSet(v_CubeSourceName, v_ViewSourceName, 1);
ViewExtractSkipZeroesSet(v_CubeSourceName, v_ViewSourceName, 0);

MDX1 = '{TM1FILTERBYLEVEL({TM1SUBSETALL(['|v_DimSourceName1|'])},0)}';
SUBSETCREATEBYMDX(v_SubSourceName1, MDX1, 0);

MDX2 = '{TM1FILTERBYLEVEL({TM1SUBSETALL(['|v_DimSourceName2|'])},0)}';
SUBSETCREATEBYMDX(v_SubSourceName2, MDX2, 0);

SubsetCreate(v_DimSourceName3, v_SubSourceName3, 0);
SubsetElementInsert(v_DimSourceName3, v_SubSourceName3, 'IFRS', 1);
SubsetElementInsert(v_DimSourceName3, v_SubSourceName3, 'STAT', 1);

SubsetCreate(v_DimSourceName4, v_SubSourceName4, 0);
SubsetElementInsert(v_DimSourceName4, v_SubSourceName4, 'Fiili', 1);

sYear = SUBST(pVersiyon, LONG(pVersiyon)-4+1, 4);
MDX5 = '{FILTER(TM1FILTERBYLEVEL(TM1DRILLDOWNMEMBER({[Period].['|sYear|']} , ALL , RECURSIVE) , 0) , [V_P].([Version].['|pVersiyon|'],[V_P_Meas].[Actual - Plan]) = "Actual")}';
SUBSETCREATEBYMDX(v_SubSourceName5, MDX5, 0);

SubsetCreate(v_DimSourceName6, v_SubSourceName6, 0);
SubsetElementInsert(v_DimSourceName6, v_SubSourceName6, 'Amortisman Çıkışlar', 1);
SubsetElementInsert(v_DimSourceName6, v_SubSourceName6, 'Dönem Amoritsmanı', 1);
SubsetElementInsert(v_DimSourceName6, v_SubSourceName6, 'Giriş Maliyet', 1);
SubsetElementInsert(v_DimSourceName6, v_SubSourceName6, 'Çıkışlar Maliyet', 1);

VIEWSUBSETASSIGN(v_CubeSourceName, v_ViewSourceName, v_DimSourceName1, v_SubSourceName1);
VIEWSUBSETASSIGN(v_CubeSourceName, v_ViewSourceName, v_DimSourceName2, v_SubSourceName2);
VIEWSUBSETASSIGN(v_CubeSourceName, v_ViewSourceName, v_DimSourceName3, v_SubSourceName3);
VIEWSUBSETASSIGN(v_CubeSourceName, v_ViewSourceName, v_DimSourceName4, v_SubSourceName4);
VIEWSUBSETASSIGN(v_CubeSourceName, v_ViewSourceName, v_DimSourceName5, v_SubSourceName5);
VIEWSUBSETASSIGN(v_CubeSourceName, v_ViewSourceName, v_DimSourceName6, v_SubSourceName6);

#Data source assignment
DataSourceType = 'View';
DataSourceNameForServer = v_CubeSourceName;
DatasourceCubeview = v_ViewSourceName;

#******* Target Cube ***********************************

v_CubeName = 'Yatırım Amortisman';
v_ViewName = 'VZO'|'_'|v_CubeName|'_'|vProcessName;

v_Dim1Name = 'Duran_Varlık_Sınıfları';
v_Dim2Name = 'Masraf_Kar_Merkezleri';
v_Dim3Name = 'Version';
v_Dim4Name = 'Period';
v_Dim5Name = 'Yatırım_Amortisman_Meas';

v_Subs1Name = v_Dim1Name|'_'|v_ViewName;
v_Subs2Name = v_Dim2Name|'_'|v_ViewName;
v_Subs3Name = v_Dim3Name|'_'|v_ViewName;
v_Subs4Name = v_Dim4Name|'_'|v_ViewName;
v_Subs5Name = v_Dim5Name|'_'|v_ViewName;

IF(VIEWEXISTS(v_CubeName, v_ViewName)=1);
    VIEWDESTROY(v_CubeName, v_ViewName);
ENDIF;

IF(SUBSETEXISTS(v_Dim1Name, v_Subs1Name)=1);
    SUBSETDESTROY(v_Dim1Name, v_Subs1Name);
ENDIF;
IF(SUBSETEXISTS(v_Dim2Name, v_Subs2Name)=1);
    SUBSETDESTROY(v_Dim2Name, v_Subs2Name);
ENDIF;
IF(SUBSETEXISTS(v_Dim3Name, v_Subs3Name)=1);
    SUBSETDESTROY(v_Dim3Name, v_Subs3Name);
ENDIF;
IF(SUBSETEXISTS(v_Dim4Name, v_Subs4Name)=1);
    SUBSETDESTROY(v_Dim4Name, v_Subs4Name);
ENDIF;
IF(SUBSETEXISTS(v_Dim5Name, v_Subs5Name)=1);
    SUBSETDESTROY(v_Dim5Name, v_Subs5Name);
ENDIF;

VIEWCREATE(v_CubeName, v_ViewName);

MDX1 = '{TM1FILTERBYLEVEL({TM1SUBSETALL(['|v_Dim1Name|'])},0)}';
SUBSETCREATEBYMDX(v_Subs1Name, MDX1, 0);

MDX2 = '{TM1FILTERBYLEVEL({TM1SUBSETALL(['|v_Dim2Name|'])},0)}';
SUBSETCREATEBYMDX(v_Subs2Name, MDX2, 0);

SubsetCreate(v_Dim3Name, v_Subs3Name, 0);
SubsetElementInsert(v_Dim3Name, v_Subs3Name, pVersiyon, 1);

MDX4 = '{FILTER(TM1FILTERBYLEVEL(TM1DRILLDOWNMEMBER({[Period].['|sYear|']} , ALL , RECURSIVE) , 0) , [V_P].([Version].['|pVersiyon|'],[V_P_Meas].[Actual - Plan]) = "Actual")}';
SUBSETCREATEBYMDX(v_Subs4Name, MDX4, 0);

SubsetCreate(v_Dim5Name, v_Subs5Name, 0);
SubsetElementInsert(v_Dim5Name, v_Subs5Name, 'Yatırım', 1);
SubsetElementInsert(v_Dim5Name, v_Subs5Name, 'Yatırım - Çıkış', 1);
SubsetElementInsert(v_Dim5Name, v_Subs5Name, 'Kanuni Amortisman - Çıkış', 1);
SubsetElementInsert(v_Dim5Name, v_Subs5Name, 'UMS Amortisman - Çıkış', 1);
SubsetElementInsert(v_Dim5Name, v_Subs5Name, 'UMS Amortisman - Mevcut', 1);

VIEWSUBSETASSIGN(v_CubeName, v_ViewName, v_Dim1Name, v_Subs1Name);
VIEWSUBSETASSIGN(v_CubeName, v_ViewName, v_Dim2Name, v_Subs2Name);
VIEWSUBSETASSIGN(v_CubeName, v_ViewName, v_Dim3Name, v_Subs3Name);
VIEWSUBSETASSIGN(v_CubeName, v_ViewName, v_Dim4Name, v_Subs4Name);
VIEWSUBSETASSIGN(v_CubeName, v_ViewName, v_Dim5Name, v_Subs5Name);

ViewZeroOut(v_CubeName, v_ViewName);
#endregion
#region Data

if (DIMIX( 'Period', ATTRS( 'Period', Period, 'PrevMonth' ) ) <> 0); 
    vPrevValue = CellGetN( 'Yatırım Giriş Fiili', Masraf_Kar_Merkezleri, DuranVarlik, Secim, 'Fiili', ATTRS( 'Period', Period, 'PrevMonth' ), Meas ); 
endif;

#Giriş Maliyet
IF (Meas @= 'Giriş Maliyet' & Secim @= 'STAT');
    IF (SUBST( Period, 6, 2 ) @= '01'); 
        CellIncrementN(Value, 'Yatırım Amortisman', DuranVarlik, Masraf_Kar_Merkezleri, pVersiyon, Period, 'Yatırım'); 
    ELSE;
        CellIncrementN(Value - vPrevValue, 'Yatırım Amortisman', DuranVarlik, Masraf_Kar_Merkezleri, pVersiyon, Period, 'Yatırım'); 
    ENDIF;
ENDIF;

#Çıkışlar Maliyet
IF (Meas @= 'Çıkışlar Maliyet' & Secim @= 'STAT');
    IF (SUBST( Period, 6, 2 ) @= '01'); 
        CellIncrementN(Value, 'Yatırım Amortisman', DuranVarlik, Masraf_Kar_Merkezleri, pVersiyon, Period, 'Yatırım - Çıkış');
    ELSE;
        CellIncrementN(Value - vPrevValue, 'Yatırım Amortisman', DuranVarlik, Masraf_Kar_Merkezleri, pVersiyon, Period, 'Yatırım - Çıkış'); 
    ENDIF;
ENDIF;

#Amortisman Çıkışlar STAT
IF (Meas @= 'Amortisman Çıkışlar' & Secim @= 'STAT');
    IF (SUBST( Period, 6, 2 ) @= '01'); 
        CellIncrementN(Value * -1, 'Yatırım Amortisman', DuranVarlik, Masraf_Kar_Merkezleri, pVersiyon, Period, 'Kanuni Amortisman - Çıkış');
    ELSE;
        CellIncrementN((Value - vPrevValue) * -1, 'Yatırım Amortisman', DuranVarlik, Masraf_Kar_Merkezleri, pVersiyon, Period, 'Kanuni Amortisman - Çıkış');
    ENDIF; 
ENDIF;

#Amortisman Çıkışlar IFRS
IF (Meas @= 'Amortisman Çıkışlar' & Secim @= 'IFRS');
    IF (SUBST( Period, 6, 2 ) @= '01'); 
        CellIncrementN(Value * -1, 'Yatırım Amortisman', DuranVarlik, Masraf_Kar_Merkezleri, pVersiyon, Period, 'UMS Amortisman - Çıkış');
    ELSE;
        CellIncrementN((Value - vPrevValue) * -1, 'Yatırım Amortisman', DuranVarlik, Masraf_Kar_Merkezleri, pVersiyon, Period, 'UMS Amortisman - Çıkış');
    ENDIF;
ENDIF;

#Dönem Amortismanı
IF (Meas @= 'Dönem Amoritsmanı' & Secim @= 'IFRS');
    IF (SUBST( Period, 6, 2 ) @= '01'); 
        CellIncrementN(Value * -1, 'Yatırım Amortisman', DuranVarlik, Masraf_Kar_Merkezleri, pVersiyon, Period, 'UMS Amortisman - Mevcut');
    ELSE;
        CellIncrementN((Value - vPrevValue) * -1, 'Yatırım Amortisman', DuranVarlik, Masraf_Kar_Merkezleri, pVersiyon, Period, 'UMS Amortisman - Mevcut');
    ENDIF;
ENDIF;
#endregion
#region Epilog

CubeName = 'Yatırım Amortisman';
CubeSetLogChanges(CubeName, 1);

###---Source View Destroy----###

IF(VIEWEXISTS(v_CubeSourceName, v_ViewSourceName)=1);
    VIEWDESTROY(v_CubeSourceName,v_ViewSourceName);
ENDIF;

IF(SUBSETEXISTS(v_DimSourceName1,v_SubSourceName1)=1);
    SUBSETDESTROY(v_DimSourceName1,v_SubSourceName1); 
ENDIF;
IF(SUBSETEXISTS(v_DimSourceName2,v_SubSourceName2)=1);
    SUBSETDESTROY(v_DimSourceName2,v_SubSourceName2); 
ENDIF;
IF(SUBSETEXISTS(v_DimSourceName3,v_SubSourceName3)=1);
    SUBSETDESTROY(v_DimSourceName3,v_SubSourceName3); 
ENDIF;
IF(SUBSETEXISTS(v_DimSourceName4,v_SubSourceName4)=1);
    SUBSETDESTROY(v_DimSourceName4,v_SubSourceName4); 
ENDIF;
IF(SUBSETEXISTS(v_DimSourceName5,v_SubSourceName5)=1);
    SUBSETDESTROY(v_DimSourceName5,v_SubSourceName5); 
ENDIF;
IF(SUBSETEXISTS(v_DimSourceName6,v_SubSourceName6)=1);
    SUBSETDESTROY(v_DimSourceName6,v_SubSourceName6); 
ENDIF;

###---Target View Destroy----###

IF(VIEWEXISTS(v_CubeName, v_ViewName)=1);
    VIEWDESTROY(v_CubeName,v_ViewName);
ENDIF;

IF(SUBSETEXISTS(v_Dim1Name,v_Subs1Name)=1);
    SUBSETDESTROY(v_Dim1Name,v_Subs1Name); 
ENDIF;
IF(SUBSETEXISTS(v_Dim2Name,v_Subs2Name)=1);
    SUBSETDESTROY(v_Dim2Name,v_Subs2Name); 
ENDIF;
IF(SUBSETEXISTS(v_Dim3Name,v_Subs3Name)=1);
    SUBSETDESTROY(v_Dim3Name,v_Subs3Name); 
ENDIF;
IF(SUBSETEXISTS(v_Dim4Name,v_Subs4Name)=1);
    SUBSETDESTROY(v_Dim4Name,v_Subs4Name); 
ENDIF;
IF(SUBSETEXISTS(v_Dim5Name,v_Subs5Name)=1);
    SUBSETDESTROY(v_Dim5Name,v_Subs5Name); 
ENDIF;
#endregion