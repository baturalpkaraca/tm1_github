#region Prolog

#Region  Prolog

#Region Process
sProcess = GetProcessName( );
sTimeStamp = TimSt( Now, '\Y\m\d\h\i\s' );
sScenario = 'Live';
nOldCubeLogChanges = CubeGetLogChanges('Salary');
CubeSetLogChanges('Salary',0);
#EndRegion Process

#Region SourceView

v_CubeNameSource='Salary';

v_ViewNameSource = '}Process_' | sProcess | '_' | sTimeStamp | 'Source';

v_Dim1NameSource='Scenario';
v_Dim2NameSource='Version';
v_Dim3NameSource='Period';
v_Dim4NameSource='Entity';
v_Dim5NameSource='Employees';
v_Dim6NameSource='Salary_M';

v_Subs1NameSource=v_Dim1NameSource|'_'|v_ViewNameSource;
v_Subs2NameSource=v_Dim2NameSource|'_'|v_ViewNameSource;
v_Subs3NameSource=v_Dim3NameSource|'_'|v_ViewNameSource;
v_Subs4NameSource=v_Dim4NameSource|'_'|v_ViewNameSource;
v_Subs5NameSource=v_Dim5NameSource|'_'|v_ViewNameSource;
v_Subs6NameSource=v_Dim6NameSource|'_'|v_ViewNameSource;

VIEWCREATE(v_CubeNameSource,v_ViewNameSource, 1);
ViewExtractSkipCalcsSet (v_CubeNameSource, v_ViewNameSource, 1);
ViewExtractSkipRuleValuesSet (v_CubeNameSource, v_ViewNameSource, 0);
ViewExtractSkipZeroesSet (v_CubeNameSource, v_ViewNameSource, 1);

SUBSETCREATEBYMDX (v_Subs1NameSource, '{[Scenario].['   | sScenario | ']}', 1);
SUBSETCREATEBYMDX (v_Subs2NameSource, '{[Version].['   | pVersion   | ']}', 1);
SUBSETCREATEBYMDX (v_Subs3NameSource, '{TM1FILTERBYLEVEL( {TM1SUBSETALL( [Period] )}, 0)}', 1);
SUBSETCREATEBYMDX (v_Subs4NameSource, '{TM1FILTERBYLEVEL( {TM1SUBSETALL( [Entity] )}, 0)}', 1);
SUBSETCREATEBYMDX (v_Subs5NameSource, '{TM1FILTERBYLEVEL( {TM1SUBSETALL( [Employees] )}, 0)}', 1);
SUBSETCREATEBYMDX (v_Subs6NameSource, '{[Salary_M].[Ücret Giriş]}', 1);

VIEWSUBSETASSIGN (v_CubeNameSource, v_ViewNameSource, v_Dim1NameSource, v_Subs1NameSource);
VIEWSUBSETASSIGN (v_CubeNameSource, v_ViewNameSource, v_Dim2NameSource, v_Subs2NameSource);
VIEWSUBSETASSIGN (v_CubeNameSource, v_ViewNameSource, v_Dim3NameSource, v_Subs3NameSource);
VIEWSUBSETASSIGN (v_CubeNameSource, v_ViewNameSource, v_Dim4NameSource, v_Subs4NameSource);
VIEWSUBSETASSIGN (v_CubeNameSource, v_ViewNameSource, v_Dim5NameSource, v_Subs5NameSource);
VIEWSUBSETASSIGN (v_CubeNameSource, v_ViewNameSource, v_Dim6NameSource, v_Subs6NameSource);

#Data source assignment
DataSourceType='View';
DataSourceNameForServer=v_CubeNameSource;
DatasourceCubeview=v_ViewNameSource;

#EndRegion SourceView
#endregion
#region Data

vScenarioVersion = CELLGETS('System_Version', vScenario, vVersion, vEntity, vPeriod, 'Input');			
IF(vScenarioVersion@='Actual'); ITEMSKIP; ENDIF;			

sYontem = CELLGETS('Employees_History', vScenario, vVersion, vEntity, vPeriod, vEmployees, 'Maaş Hesaplama Yöntemi' );			
IF(sYontem @<>'Net'); ITEMSKIP; ENDIF;			

vMaaş1 =  CellGetN( 'Salary', vScenario, vVersion, vPeriod, vEntity, vEmployees, 'Ücret Giriş' );
vMaaş2 = vMaaş1 ;
i = 1;
WHILE(i<50);

    CellPutN(vMaaş2, 'Salary', vScenario, vVersion, vPeriod, vEntity, vEmployees, 'Brüt Kazançlar Hesaplama' );
    vNetMaaş = CellGetN( 'Salary', vScenario, vVersion, vPeriod, vEntity, vEmployees, 'Salary_M':'Net Maaş' );
    CellPutN(i,  'Salary', vScenario, vVersion, vPeriod, vEntity, vEmployees, 'Iterasyon' );

    IF(ABS( vMaaş1 - vNetMaaş ) < 0.01);
        i=100;
    ELSE;
        vMaaş2 = vMaaş2 + (vMaaş1 - vNetMaaş);  
    ENDIF;
    i = i + 1;
END;

#endregion
#region Epilog
CubeSetLogChanges('Salary',nOldCubeLogChanges);
#endregion