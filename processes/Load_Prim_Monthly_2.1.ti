#region Prolog
#****Begin: Generated Statements***
#****End: Generated Statements****

CubeName = 'Prim Reyon';
CubeSetLogChanges (CubeName, 0);

#******* Source Cube ***********************************

vProcessName = GetProcessName;

v_CubeSourceName = 'MM_P';
v_ViewSourceName = 'Source'|'_'|v_CubeSourceName|'_'|vProcessName;

v_DimSourceName1 = 'Masraf_Kar_Merkezleri';
v_DimSourceName2 = 'Period';
v_DimSourceName3 = 'MM_P_Meas';

v_SubSourceName1 = v_DimSourceName1|'_'|v_ViewSourceName;
v_SubSourceName2 = v_DimSourceName2|'_'|v_ViewSourceName;
v_SubSourceName3 = v_DimSourceName3|'_'|v_ViewSourceName;

IF(VIEWEXISTS(v_CubeSourceName, v_ViewSourceName)=1);
    VIEWDESTROY(v_CubeSourceName, v_ViewSourceName);
ENDIF;

IF(SUBSETEXISTS(v_DimSourceName1, v_SubSourceName1)=1);
    SUBSETDESTROY(v_DimSourceName1, v_SubSourceName1); 
ENDIF;
IF(SUBSETEXISTS(v_DimSourceName2, v_SubSourceName2)=1);
    SUBSETDESTROY(v_DimSourceName2, v_SubSourceName2); 
ENDIF;
IF(SUBSETEXISTS(v_DimSourceName3, v_SubSourceName3)=1);
    SUBSETDESTROY(v_DimSourceName3, v_SubSourceName3); 
ENDIF;

VIEWCREATE(v_CubeSourceName, v_ViewSourceName);

ViewExtractSkipCalcsSet(v_CubeSourceName, v_ViewSourceName, 1);
ViewExtractSkipRuleValuesSet(v_CubeSourceName, v_ViewSourceName, 1);
ViewExtractSkipZeroesSet(v_CubeSourceName, v_ViewSourceName, 1);

MDX1 = '{TM1FILTERBYLEVEL({TM1SUBSETALL(['|v_DimSourceName1|'])},0)}';
SUBSETCREATEBYMDX(v_SubSourceName1, MDX1, 0);
SubsetMDXSet(v_DimSourceName1, v_SubSourceName1, '' );

MDX2 = '{TM1FILTERBYLEVEL({TM1DRILLDOWNMEMBER({ ['|v_DimSourceName2|'].['|pPeriod|']},ALL,RECURSIVE)}, 0)}';
SUBSETCREATEBYMDX (v_SubSourceName2, MDX2, 0);
SubsetMDXSet(v_DimSourceName2, v_SubSourceName2, '' );

SubsetCreate(v_DimSourceName3, v_SubSourceName3, 0);
SubsetElementInsert(v_DimSourceName3, v_SubSourceName3, 'Perakende Satış', 1);
SubsetElementInsert(v_DimSourceName3, v_SubSourceName3, 'Ticari Satış', 1);
SubsetElementInsert(v_DimSourceName3, v_SubSourceName3, 'Mobil Ticari', 1);
SubsetElementInsert(v_DimSourceName3, v_SubSourceName3, '3P Satış', 1);
SubsetElementInsert(v_DimSourceName3, v_SubSourceName3, 'Handyman Sales', 1);
SubsetElementInsert(v_DimSourceName3, v_SubSourceName3, 'Kiosk Sales', 1);

VIEWSUBSETASSIGN(v_CubeSourceName, v_ViewSourceName, v_DimSourceName1, v_SubSourceName1);
VIEWSUBSETASSIGN(v_CubeSourceName, v_ViewSourceName, v_DimSourceName2, v_SubSourceName2);
VIEWSUBSETASSIGN(v_CubeSourceName, v_ViewSourceName, v_DimSourceName3, v_SubSourceName3);

#Data source assignment
DataSourceType = 'View';
DataSourceNameForServer = v_CubeSourceName;
DatasourceCubeview = v_ViewSourceName;

#******* Target Cube ***********************************

vProcessName = GetProcessName;

v_CubeName = 'Prim Reyon';
v_ViewName = 'VZO'|'_'|v_CubeName|'_'|vProcessName;

v_Dim1Name = 'Reyon_Acc';
v_Dim2Name = 'Mal_Grupları';
v_Dim3Name = 'Masraf_Kar_Merkezleri';
v_Dim4Name = 'Period';
v_Dim5Name = 'Prim_Reyon_Meas';

v_Subs1Name = v_Dim1Name|'_'|v_ViewName;
v_Subs2Name = v_Dim2Name|'_'|v_ViewName;
v_Subs3Name = v_Dim3Name|'_'|v_ViewName;
v_Subs4Name = v_Dim4Name|'_'|v_ViewName;
v_Subs5Name = v_Dim5Name|'_'|v_ViewName;

IF(VIEWEXISTS(v_CubeName, v_ViewName)=1);
    VIEWDESTROY(v_CubeName, v_ViewName);
ENDIF;

IF(SUBSETEXISTS(v_Dim1Name, v_Subs1Name)=1);
    SUBSETDESTROY(v_Dim1Name, v_Subs1Name);
ENDIF;
IF(SUBSETEXISTS(v_Dim2Name, v_Subs2Name)=1);
    SUBSETDESTROY(v_Dim2Name, v_Subs2Name);
ENDIF;
IF(SUBSETEXISTS(v_Dim3Name, v_Subs3Name)=1);
    SUBSETDESTROY(v_Dim3Name, v_Subs3Name);
ENDIF;
IF(SUBSETEXISTS(v_Dim4Name, v_Subs4Name)=1);
    SUBSETDESTROY(v_Dim4Name, v_Subs4Name);
ENDIF;
IF(SUBSETEXISTS(v_Dim5Name, v_Subs5Name)=1);
    SUBSETDESTROY(v_Dim5Name, v_Subs5Name);
ENDIF;

VIEWCREATE(v_CubeName, v_ViewName);

MDX1 = '{TM1FILTERBYLEVEL({TM1SUBSETALL(['|v_Dim1Name|'])},0)}';
SUBSETCREATEBYMDX(v_Subs1Name, MDX1, 0);
SubsetMDXSet(v_Dim1Name, v_Subs1Name, '' );

MDX2 = '{TM1FILTERBYLEVEL({TM1DRILLDOWNMEMBER({['|v_Dim2Name|'].[Koçtaş Kategori Toplam]},ALL,RECURSIVE)},0)}';
SUBSETCREATEBYMDX(v_Subs2Name, MDX2, 0);
SubsetMDXSet(v_Dim2Name, v_Subs2Name, '' );

MDX3 = '{TM1FILTERBYLEVEL({TM1SUBSETALL(['|v_Dim3Name|'])},0)}';
SUBSETCREATEBYMDX(v_Subs3Name, MDX3, 0);
SubsetMDXSet(v_Dim3Name, v_Subs3Name, '' );

MDX4 = '{TM1FILTERBYLEVEL({TM1DRILLDOWNMEMBER({ ['|v_Dim4Name|'].['|pPeriod|']},ALL,RECURSIVE)}, 0)}';
SUBSETCREATEBYMDX(v_Subs4Name, MDX4, 0);
SubsetMDXSet(v_Dim4Name, v_Subs4Name, '' );

SubsetCreate(v_Dim5Name, v_Subs5Name, 0);
SubsetElementInsert(v_Dim5Name, v_Subs5Name, 'Amount', 1);

VIEWSUBSETASSIGN(v_CubeName, v_ViewName, v_Dim1Name, v_Subs1Name);
VIEWSUBSETASSIGN(v_CubeName, v_ViewName, v_Dim2Name, v_Subs2Name);
VIEWSUBSETASSIGN(v_CubeName, v_ViewName, v_Dim3Name, v_Subs3Name);
VIEWSUBSETASSIGN(v_CubeName, v_ViewName, v_Dim4Name, v_Subs4Name);
VIEWSUBSETASSIGN(v_CubeName, v_ViewName, v_Dim5Name, v_Subs5Name);

ViewZeroOut(v_CubeName, v_ViewName);

MDX = '{TM1FILTERBYLEVEL({TM1DRILLDOWNMEMBER({ [Mal_Grupları].[Koçtaş Kategori Toplam]},ALL,RECURSIVE)}, 0)}';
SubsetCreatebyMDX( 'Temp', MDX, 0 );
SubsetMDXSet( 'Mal_Grupları', 'Temp', '' );
#endregion
#region Metadata
#****Begin: Generated Statements***
#****End: Generated Statements****
#endregion
#region Data
#****Begin: Generated Statements***
#****End: Generated Statements****

vMM_Yontem = CellGetS( 'MM_P', Masraf_Kar_Merkezleri,Period,'Yöntem '|Meas );

vRef_MM = Masraf_Kar_Merkezleri;

if (vMM_Yontem @= 'Bigbox'); 
    vRef_MM = '1340000'; 
elseif( vMM_Yontem @= 'Fix'  );
    vRef_MM = '6340100'; 
endif;

vRefMeas = Meas;

# if (Meas @= 'Mobil Ticari'); 
#     vRefMeas = 'Perakende Satış'; 
# endif;

vToplamReyonAmount = CellGetN( 'Final External Satis', 'Koçtaş Kategori Toplam', vRef_MM, Period, pVersiyon, vRefMeas );

Size = SubsetGetSize('Mal_Grupları', 'Temp');
i=1;

While(i<=Size);

    vReyon = SubsetGetElementName('Mal_Grupları', 'Temp', i);

    vReyonAmount = CellGetN( 'Final External Satis', vReyon, vRef_MM, Period, pVersiyon, vRefMeas );

    vOran = vReyonAmount \ vToplamReyonAmount;

    CellPutN( Value * vOran, 'Prim Reyon', Meas, vReyon, Masraf_Kar_Merkezleri, Period, 'Amount' );

    i=i+1;
END;
#endregion
#region Epilog
#****Begin: Generated Statements***
#****End: Generated Statements****

CubeName = 'Prim Reyon';
CubeSetLogChanges (CubeName, 1);

IF(SUBSETEXISTS('Mal_Grupları','Temp')=1);
    SUBSETDESTROY('Mal_Grupları','Temp'); 
ENDIF;

###---Source View Destroy----###

IF(VIEWEXISTS(v_CubeSourceName, v_ViewSourceName)=1);
    VIEWDESTROY(v_CubeSourceName,v_ViewSourceName);
ENDIF;

IF(SUBSETEXISTS(v_DimSourceName1,v_SubSourceName1)=1);
    SUBSETDESTROY(v_DimSourceName1,v_SubSourceName1); 
ENDIF;
IF(SUBSETEXISTS(v_DimSourceName2,v_SubSourceName2)=1);
    SUBSETDESTROY(v_DimSourceName2,v_SubSourceName2); 
ENDIF;
IF(SUBSETEXISTS(v_DimSourceName3,v_SubSourceName3)=1);
    SUBSETDESTROY(v_DimSourceName3,v_SubSourceName3); 
ENDIF;

###---Target View Destroy----###

IF(VIEWEXISTS(v_CubeName, v_ViewName)=1);
    VIEWDESTROY(v_CubeName,v_ViewName);
ENDIF;

IF(SUBSETEXISTS(v_Dim1Name,v_Subs1Name)=1);
    SUBSETDESTROY(v_Dim1Name,v_Subs1Name); 
ENDIF;
IF(SUBSETEXISTS(v_Dim2Name,v_Subs2Name)=1);
    SUBSETDESTROY(v_Dim2Name,v_Subs2Name); 
ENDIF;
IF(SUBSETEXISTS(v_Dim3Name,v_Subs3Name)=1);
    SUBSETDESTROY(v_Dim3Name,v_Subs3Name); 
ENDIF;
IF(SUBSETEXISTS(v_Dim4Name,v_Subs4Name)=1);
    SUBSETDESTROY(v_Dim4Name,v_Subs4Name); 
ENDIF;
IF(SUBSETEXISTS(v_Dim5Name,v_Subs5Name)=1);
    SUBSETDESTROY(v_Dim5Name,v_Subs5Name); 
ENDIF;

#endregion